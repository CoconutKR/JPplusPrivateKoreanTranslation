spriteset (kb_kg_5000series_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(kb_kg_5000series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series_purchase.png"){ tmpl_purchase(0, 0) }
//livery 1
spriteset (kb_kg_5000series_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (kb_kg_5000series_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }
spriteset (kb_kg_5000series_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (kb_kg_5000series_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (kb_kg_5000series_front_f,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (kb_kg_5000series_end_f,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }

alternative_sprites(kb_kg_5000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series.png"){ tmpl_std(0, 0) }
alternative_sprites(kb_kg_5000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(kb_kg_5000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series.png"){ tmpl_std(0, 75) }
alternative_sprites(kb_kg_5000series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series.png"){ tmpl_std(0, 50) }
alternative_sprites(kb_kg_5000series_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series.png"){ tmpl_std(0, 100) }
alternative_sprites(kb_kg_5000series_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/Kobe/kaigan/5000series/5000series.png"){ tmpl_std_rev(0, 100) }

//livery 1
//panto placer
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_panto_placer_1,position_in_consist %4){
    3: kb_kg_5000series_mot;
    kb_kg_5000series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_panto_placer_1_rev,position_in_consist_from_end %7){
    3: kb_kg_5000series_mot;
    kb_kg_5000series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kb_kg_5000series_panto_placer_1_main,vehicle_is_reversed){
    1: sw_kb_kg_5000series_panto_placer_1_rev;
    sw_kb_kg_5000series_panto_placer_1;
}
//panto main
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_panto_placer_main,num_vehs_in_consist >5){
    1:sw_kb_kg_5000series_panto_placer_1_main;
    kb_kg_5000series_wag;
}
//panto placer end 
//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_fake_placer,position_in_consist){
    3: kb_kg_5000series_end_f;
    4: kb_kg_5000series_front_f;
sw_kb_kg_5000series_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_fake_placer_rev,position_in_consist_from_end){
    3: kb_kg_5000series_front_f;
    4: kb_kg_5000series_end_f;
sw_kb_kg_5000series_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_kb_kg_5000series_fake_placer_gfx,vehicle_is_reversed){
    1: sw_kb_kg_5000series_fake_placer_rev;
    sw_kb_kg_5000series_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_fake_placer_main,num_vehs_in_consist>7){
    1: sw_kb_kg_5000series_fake_placer_gfx;
sw_kb_kg_5000series_panto_placer_main;
}
//fake head-end placer end
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_end,position_in_consist_from_end){
    0: kb_kg_5000series_end;
    1: kb_kg_5000series_wag;
    default: sw_kb_kg_5000series_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_livery_1,position_in_consist){
    0: kb_kg_5000series_front;
    1: kb_kg_5000series_wag;
    default: sw_kb_kg_5000series_end;
}
//front-end-fixer end

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_kb_kg_5000series_main_capacity,position_in_consist_from_end){
    0: 84*param_capacity_mod/3;
    default: 97*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, kb_kg_5000series_attach_vehid, vehicle_type_id) {
  mu_car: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}


item(FEAT_TRAINS, kb_kg_5000series,1447) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_kobe == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_kb_kg_5000series_NAME);
        introduction_date: date(2000, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 20;

        cost_factor: 19;
        running_cost_factor: 49;
        speed: 80 km/h;
        track_type: TR_1435;
        power: 270 kW;
        weight: 27 ton;
        loading_speed: param_loading_3D;
    }
	graphics {
        default: sw_kb_kg_5000series_livery_1;
        purchase: kb_kg_5000series_purchase;
        can_attach_wagon: kb_kg_5000series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_4_CARS),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_KOBE,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_KOBE_KAIGAN)
                            ),
                        );
        start_stop: sw_stop_start_3; 
        cargo_capacity: 84*param_capacity_mod/3;  
    }
    livery_override(mu_car){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_kb_kg_5000series_main_capacity;
        default:                      sw_kb_kg_5000series_livery_1;
        power: 362;
        weight: 25;
        length: 8; 
      }
}

