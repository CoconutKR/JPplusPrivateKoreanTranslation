spriteset (tk_hz_08series_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_hz_08series_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_hz_08series_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_mot_1,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_mot_1_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
alternative_sprites(tk_hz_08series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_hz_08series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_hz_08series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std(0, 50) }
alternative_sprites(tk_hz_08series_mot_1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std(0, 100) }
alternative_sprites(tk_hz_08series_mot_1_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std(0, 100) }
alternative_sprites(tk_hz_08series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std(0, 75) }
alternative_sprites(tk_hz_08series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std_rev(0, 25) }

//livery 1
//panto-placers
switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_mot_def_gfx,position_in_consist %10){
    2: tk_hz_08series_mot;
    5: tk_hz_08series_mot_1;
    8: tk_hz_08series_mot;
    default: tk_hz_08series_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_mot_def_rev_gfx,position_in_consist_from_end %10){
    2: tk_hz_08series_mot;
    5: tk_hz_08series_mot_1_rev;
    8: tk_hz_08series_mot;
    default: tk_hz_08series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tk_hz_08series_mot_def_main_gfx,vehicle_is_reversed){
    1: sw_tk_hz_08series_mot_def_rev_gfx;
    sw_tk_hz_08series_mot_def_gfx;
}
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_end_gfx,position_in_consist_from_end){
    0: tk_hz_08series_end;
    default: sw_tk_hz_08series_mot_def_main_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_front_gfx,position_in_consist){
    0: tk_hz_08series_front;
    default: sw_tk_hz_08series_end_gfx;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_main_capacity,position_in_consist_from_end){
    0: 141*param_capacity_mod/3;
    default: 152*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_hz_08series_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, tk_hz_08series,1101) {
    property {
        name: string(STR_tk_hz_08series_NAME);
        introduction_date: date(2002, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 22;

        cargo_capacity: 141;
        loading_speed: param_loading_4D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 110 km/h;
        track_type: TR_1067;
        power: 660 kW;
        weight: 27 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        default: sw_tk_hz_08series_front_gfx;
        purchase: tk_hz_08series_purchase;
        can_attach_wagon: tk_hz_08series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_10_CARS),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_TOKYOMETRO,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_HANZO)
                            ),
                        );
        start_stop: sw_stop_start_4;   
        cargo_capacity: 141*param_capacity_mod/3;
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tk_hz_08series_main_capacity;
        default:                      sw_tk_hz_08series_front_gfx;
        power: 885*4/9;
        weight: 28;
        length: 8;
      }
}