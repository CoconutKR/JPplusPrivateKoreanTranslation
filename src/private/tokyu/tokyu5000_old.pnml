spriteset (tokyu5000_old_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tokyu5000_old_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (tokyu5000_old_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu5000_old_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }
spriteset (tokyu5000_old_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tokyu5000_old_mot_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tokyu5000_old_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tokyu5000_old_front_f,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu5000_old_end_f,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }

alternative_sprites(tokyu5000_old_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_std(0, 0) }
alternative_sprites(tokyu5000_old_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tokyu5000_old_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_std(0, 50) }
alternative_sprites(tokyu5000_old_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(tokyu5000_old_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_symm_std(0, 75) }
alternative_sprites(tokyu5000_old_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_std(0, 25) }
alternative_sprites(tokyu5000_old_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tokyu/tokyu5000_old/old5000.png"){ tmpl_std_rev(0, 25) }

//*livery 1
//special gfx
//panto placer
//1
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_panto_placer,position_in_consist %3){
    0: tokyu5000_old_mot;
    tokyu5000_old_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_panto_placer_rev,position_in_consist_from_end %3){
    0: tokyu5000_old_mot_rev;
    tokyu5000_old_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu5000_old_panto_placer_main_1,vehicle_is_reversed){
    1: sw_tokyu5000_old_panto_placer_rev;
    sw_tokyu5000_old_panto_placer;
}
//2
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_1_panto_placer,position_in_consist %5){
    2: tokyu5000_old_mot;
    tokyu5000_old_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_1_panto_placer_rev,position_in_consist_from_end %5){
    2: tokyu5000_old_mot_rev;
    tokyu5000_old_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu5000_old_panto_placer_main_2,vehicle_is_reversed){
    1: sw_tokyu5000_old_1_panto_placer_rev;
    sw_tokyu5000_old_1_panto_placer;
}

switch(FEAT_TRAINS, PARENT, sw_tokyu5000_old_panto_placer_main,num_vehs_in_consist){
5..6:sw_tokyu5000_old_panto_placer_main_2;
sw_tokyu5000_old_panto_placer_main_1;
}
//panto placer end 

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_fake_placer,position_in_vehid_chain+1){
    3: tokyu5000_old_end_f;
    4: tokyu5000_old_front_f;
sw_tokyu5000_old_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_fake_placer_rev,position_in_vehid_chain_from_end){
    3: tokyu5000_old_front_f;
    4: tokyu5000_old_end_f;
sw_tokyu5000_old_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu5000_old_fake_placer_gfx,vehicle_is_reversed){
    1: sw_tokyu5000_old_fake_placer_rev;
    sw_tokyu5000_old_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_fake_placer_main,num_vehs_in_vehid_chain>6){
    1: sw_tokyu5000_old_fake_placer_gfx;
sw_tokyu5000_old_panto_placer_main;
}
//fake head-end placer end

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_end_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu5000_old_end;
    1: tokyu5000_old_wag;
    default: sw_tokyu5000_old_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_front_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu5000_old_front;
    default: sw_tokyu5000_old_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_livery_0,vehicle_type_id){
    mu_car_narrow:sw_tokyu5000_old_end_gfx_main;
    mu_car:sw_tokyu5000_old_end_gfx_main;
    sw_tokyu5000_old_front_gfx_main;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_capacity,position_in_vehid_chain_from_end){
    0: 150*param_capacity_mod/3;
    default: 140*param_capacity_mod/3;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_capacity_1,position_in_vehid_chain){
    0: 150*param_capacity_mod/3;
    default: sw_tokyu5000_old_capacity;
}
switch(FEAT_TRAINS, SELF, sw_tokyu5000_old_capacity_main,vehicle_type_id){
    mu_car_narrow:sw_tokyu5000_old_capacity;
    mu_car:sw_tokyu5000_old_capacity;
    sw_tokyu5000_old_capacity_1;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, tokyu5000_old_attach_vehid, vehicle_type_id) {
  mu_car: return string(STR_ATTACH_1067_ERROR);
  mu_car_metro: return string(STR_ATTACH_1067_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tokyu5000_old,2005) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_tokyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_tokyu5000_old_NAME);
        introduction_date: date(1954, 1, 1);
        model_life: 32;
        vehicle_life: 32;
        reliability_decay: 20;

        cost_factor: 18;
        running_cost_factor: 48;
        speed: 100 km/h;
        track_type: TR_1067_DC;
        power: 440 kW;
        weight: 23 ton;
        loading_speed: param_loading_3D;
    }
	graphics {
        default: sw_tokyu5000_old_livery_0;
        purchase: tokyu5000_old_purchase;
        can_attach_wagon: tokyu5000_old_attach_vehid;
        additional_text:    string(STR_CONCAT_8,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_SLIDE_3),),
                            string(STR_FORMATION),
                            string(STR_tokyu5000_old_DESC),
                            string(STR_CARS),
                            string(STR_COUPLER,
                            string(STR_KNUCKLE)),
                            string(STR_USAGE,
                            string(STR_COMMUTER),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR_PRIVATE,
                            string(STR_TOKYU),
                            ),
                            
                        );
        start_stop: sw_stop_start_2; 
        cargo_capacity: sw_tokyu5000_old_capacity_main;  
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_tokyu5000_old_capacity_main;
        default:                      sw_tokyu5000_old_livery_0;
        power: 590/3;
        weight: 23;
        length: 8; 
      }
}