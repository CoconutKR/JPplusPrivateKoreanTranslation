spriteset (tobu1700_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tobu1700_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700_purchase.png"){ tmpl_purchase(0, 0) }
//livery 1
    spriteset (tobu1700_front_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
    spriteset (tobu1700_end_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
    spriteset (tobu1700_end_mot_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
    spriteset (tobu1700_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
    spriteset (tobu1700_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
    spriteset (tobu1700_end_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }

    alternative_sprites(tobu1700_front_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700.png"){ tmpl_std(0, 0) }
    alternative_sprites(tobu1700_end_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700.png"){ tmpl_std_rev(0, 25) }
    alternative_sprites(tobu1700_end_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700.png"){ tmpl_std(0, 25) }
    alternative_sprites(tobu1700_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700.png"){ tmpl_std(0, 50) }
    alternative_sprites(tobu1700_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700.png"){ tmpl_std_rev(0, 75) }
    alternative_sprites(tobu1700_end_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/tobu1700/tobu1700.png"){ tmpl_std(0, 75) }

//*livery 1
  //special gfx
    //front&end
    switch(FEAT_TRAINS, PARENT, sw_tobu1700_front_gfx, vehicle_is_reversed){
        1: tobu1700_front;
        tobu1700_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_tobu1700_end_gfx, vehicle_is_reversed){
        1: tobu1700_end_mot;
        tobu1700_end;
    }

  //panto placer
    switch(FEAT_TRAINS, SELF, sw_tobu1700_panto_placer,position_in_vehid_chain %2){
        0:tobu1700_end;
        tobu1700_end_mot_rev;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu1700_panto_placer_rev,1-position_in_vehid_chain_from_end %2){
        0: tobu1700_end_rev;
        tobu1700_end_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_tobu1700_panto_placer_main,vehicle_is_reversed){
        1: sw_tobu1700_panto_placer_rev;
        sw_tobu1700_panto_placer;
    }
  //end car fixer
    switch(FEAT_TRAINS, SELF, sw_tobu1700_end_fix, position_in_vehid_chain_from_end){
        0:sw_tobu1700_end_gfx;
        sw_tobu1700_panto_placer_main;
    }

    switch(FEAT_TRAINS, SELF, sw_tobu1700_lv,vehicle_type_id){
        mu_car_narrow:sw_tobu1700_end_fix;
        sw_tobu1700_front_gfx;
    }

  //attachable vehicles
    switch(FEAT_TRAINS, SELF, tobu1700_attach_vehid, vehicle_type_id) {
    mu_car_narrow:  return CB_RESULT_ATTACH_ALLOW;
    return string(STR_ATTACH_ERROR);
    }

//power fixer
    switch(FEAT_TRAINS, SELF, tobu1700_power, position_in_vehid_chain %2) {
        1: return 75*4*1342/1000;
    return 0;
}

item(FEAT_TRAINS, tobu1700,2051) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_tobu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_tobu1700_NAME);
        introduction_date: date(1956, 1, 1);
        vehicle_life: 35;
        model_life: 30;
        reliability_decay: 20;

        cost_factor: 19;
        running_cost_factor: 49;
        speed: 105 km/h;
        track_type: TR_1067_DC;
        power: 300 kW;
        weight: 36 ton;
        loading_speed: param_loading_1D;
    }
	graphics {
        start_stop: sw_stop_start_2;
        default: sw_tobu1700_lv;
        purchase: tobu1700_purchase;
        can_attach_wagon: tobu1700_attach_vehid;
        //cargo_subtype_text:sw_tobu1700_lv_desc;
        additional_text:    string(STR_CONCAT_8,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_1_DOOR),),
                            string(STR_FORMATION),
                            string(STR_tobu5000s_DESC),
                            string(STR_CARS),
                            string(STR_COUPLER,
                            string(STR_KNUCKLE)),
                            string(STR_USAGE,
                            string(STR_CONCAT_2_OR,
                            string(STR_EXPRESS),
                            string(STR_LIMITED)),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR_PRIVATE,
                            string(STR_TOBU),
                            ),
                            
                        );
        cargo_age_period:ltd_express_cargo_age;                 
        cargo_capacity: 56*param_capacity_mod/3;  
    }
    livery_override(mu_car_narrow){
        cargo_age_period:ltd_express_cargo_age;
        loading_speed: param_loading_1D;
        cargo_capacity: 56*param_capacity_mod/3;
        default:                      sw_tobu1700_lv;
        power: tobu1700_power;
        weight: 35;
      }
}