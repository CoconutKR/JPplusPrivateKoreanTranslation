spriteset (tobu_deha1_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tobu_deha1_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/deha1/tobu_deha1_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (tobu_deha1_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu_deha1_front_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu_deha1_end,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tobu_deha1_end_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tobu_deha1_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }

alternative_sprites(tobu_deha1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/deha1/tobu_deha1.png"){ tmpl_std(0, 0) }
alternative_sprites(tobu_deha1_front_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/deha1/tobu_deha1.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tobu_deha1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/deha1/tobu_deha1.png"){ tmpl_std(0, 50) }
alternative_sprites(tobu_deha1_end_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/deha1/tobu_deha1.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(tobu_deha1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/tobu/deha1/tobu_deha1.png"){ tmpl_symm_std(0, 75) }

//front-end-fixer
switch(FEAT_TRAINS, PARENT, sw_tobu_deha1_front_gfx, vehicle_is_reversed){
    1:tobu_deha1_front_rev;
    tobu_deha1_front;
}

switch(FEAT_TRAINS, PARENT, sw_tobu_deha1_end_gfx, vehicle_is_reversed){
    1:tobu_deha1_end_rev;
    tobu_deha1_end;
}
//panto placer
switch(FEAT_TRAINS, SELF, sw_tobu_deha1_panto_placer,position_in_consist %2){
    0: sw_tobu_deha1_end_gfx;
    tobu_deha1_wag;
}
switch(FEAT_TRAINS, SELF, sw_tobu_deha1_panto_placer_rev,position_in_consist_from_end %2){
    0: sw_tobu_deha1_end_gfx;
    tobu_deha1_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tobu_deha1_panto_placer_main,vehicle_is_reversed){
    1: sw_tobu_deha1_panto_placer_rev;
    sw_tobu_deha1_panto_placer;
}
//end car fixer
switch(FEAT_TRAINS, SELF, sw_tobu_deha1_end_fix, position_in_consist_from_end){
    0:sw_tobu_deha1_end_gfx;
    sw_tobu_deha1_panto_placer_main;
}

switch(FEAT_TRAINS, SELF, sw_tobu_deha1_livery_0,vehicle_type_id){
    mu_car_narrow:sw_tobu_deha1_end_fix;
    sw_tobu_deha1_front_gfx;
}

//attachable vehicles
switch(FEAT_TRAINS, SELF, tobu_deha1_attach_vehid, vehicle_type_id) {
  mu_car_narrow:  return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, tobu_deha1,2065) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_tobu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_tobu_deha1_NAME);
        introduction_date: date(1924, 1, 1);
        model_life: 20;
        vehicle_life: 46;
        reliability_decay: 20;

        cost_factor: 12;
        running_cost_factor: 42;
        speed: 75 km/h;
        track_type: TR_1067_DC;
        power: 240 kW;
        weight: 33 ton;
        loading_speed: param_loading_2D;
    }
	graphics {
        default: sw_tobu_deha1_livery_0;
        purchase: tobu_deha1_purchase;
        can_attach_wagon: tobu_deha1_attach_vehid;
        additional_text:    string(STR_CONCAT_8,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_2_DOOR),),
                            string(STR_FORMATION),
                            string(STR_1_CARS),
                            string(STR_CARS),
                            string(STR_COUPLER,
                            string(STR_KNUCKLE)),
                            string(STR_USAGE,
                            string(STR_COMMUTER),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR_PRIVATE,
                            string(STR_TOBU),
                            ),
                            
                        );
                         
        cargo_capacity: 92*param_capacity_mod/3;  
        length: 7; 
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_2D;
        cargo_capacity: 92*param_capacity_mod/3;
        default:                      sw_tobu_deha1_livery_0;
        power: 240*1342/2000;
        weight: 33;
        length: 7; 
      }
}