//purchase sprite
  spriteset (hankyu6300_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
  alternative_sprites(hankyu6300_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/hankyu/hankyu6000/hankyu6300_purchase.png"){ tmpl_purchase(0, 0) }
//livery 1
  spriteset (hankyu6300_1_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
  spriteset (hankyu6300_1_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
  spriteset (hankyu6300_1_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
  spriteset (hankyu6300_1_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
  
  alternative_sprites(hankyu6300_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/hankyu/hankyu6000/hankyu6300.png"){ tmpl_high_skirt(0, 0) }
  alternative_sprites(hankyu6300_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/hankyu/hankyu6000/hankyu6300.png"){ tmpl_high_skirt_rev(0, 25) }
  alternative_sprites(hankyu6300_1_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/hankyu/hankyu6000/hankyu6300.png"){ tmpl_symm(0, 50) }
  alternative_sprites(hankyu6300_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/hankyu/hankyu6000/hankyu6300.png"){ tmpl_symm(0, 75) }

//*livery 1
  //panto-placers
    //var 1
      switch(FEAT_TRAINS, SELF, sw_hankyu6300_1_panto_placer_1,position_in_vehid_chain %2){
        0: hankyu6300_1_mot;
        hankyu6300_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_hankyu6300_1_panto_placer_1_rev,position_in_vehid_chain_from_end %2-1){
        0: hankyu6300_1_mot;
        hankyu6300_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_hankyu6300_1_panto_placer_main_1,vehicle_is_reversed){
        1: sw_hankyu6300_1_panto_placer_1_rev;
        sw_hankyu6300_1_panto_placer_1;
      }
    //var 2
      switch(FEAT_TRAINS, SELF, sw_hankyu6300_1_panto_placer_2,position_in_vehid_chain %4){
        0: hankyu6300_1_mot;
        hankyu6300_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_hankyu6300_1_panto_placer_2_rev,position_in_vehid_chain_from_end %4-1){
        0: hankyu6300_1_mot;
        hankyu6300_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_hankyu6300_1_panto_placer_main_2,vehicle_is_reversed){
        1: sw_hankyu6300_1_panto_placer_2_rev;
        sw_hankyu6300_1_panto_placer_2;
      }
    //main
      switch(FEAT_TRAINS, SELF, sw_hankyu6300_1_panto_placer_main,num_vehs_in_vehid_chain+1){
        4..6:sw_hankyu6300_1_panto_placer_main_1;
        sw_hankyu6300_1_panto_placer_main_2;
      }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_hankyu6300_1_gfx_main,position_in_vehid_chain_from_end){
      0: hankyu6300_1_end;
      default: sw_hankyu6300_1_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_hankyu6300_lv,vehicle_type_id){
      hankyu6300: hankyu6300_1_front;
      default: sw_hankyu6300_1_gfx_main;
    }

//*post fixers
  //capacity-changer
  switch(FEAT_TRAINS, SELF, sw_hankyu6300_capacity,position_in_vehid_chain_from_end){
    0: 129;
    default: 141;
  }
  switch(FEAT_TRAINS, SELF, sw_hankyu6300_capacity_1,position_in_vehid_chain){
    0: 129;
    default: sw_hankyu6300_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_hankyu6300_capacity_main,vehicle_type_id){
    mu_car: sw_hankyu6300_capacity;
    sw_hankyu6300_capacity_1;
  }
  //power fix
  switch(FEAT_TRAINS, SELF, sw_hankyu6300_power_main_1,position_in_vehid_chain_from_end%8){
    1..2:return 140;
    5:return 140;
    return 0;
  }
  switch(FEAT_TRAINS, SELF, sw_hankyu6300_power_main,vehicle_type_id){
    hankyu6300: return 140;
    sw_hankyu6300_power_main_1;
  }
//
item(FEAT_TRAINS, hankyu6300) {
    property {
        name: string(STR_hankyu6300_NAME);
        introduction_date: date(1975, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 20;

        cost_factor: 21;
        running_cost_factor: 50;

        speed: 120 km/h;
        track_type: TR_1435_DC;
        weight: 30 ton;
        climates_available: param_disable_hankyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        //bulk
          cargo_capacity: 1;
          power: 1 kW;
          ai_special_flag: AI_FLAG_PASSENGER;
          sprite_id: SPRITE_ID_NEW_TRAIN;
          misc_flags: bitmask(TRAIN_FLAG_MU);
          refittable_cargo_classes: bitmask(CC_PASSENGERS);

          running_cost_base: RUNNING_COST_ELECTRIC;
          engine_class: ENGINE_CLASS_ELECTRIC;
          visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
          
          tractive_effort_coefficient: 0.3;
          air_drag_coefficient: 0.07;
          bitmask_vehicle_info: 0;
          length: 8;
    }
	graphics {
    purchase: hankyu6300_purchase;
		default: sw_hankyu6300_lv;
		start_stop: sw_stop_start_3;
		power: sw_hankyu6300_power_main()*4*1342/1000;
		loading_speed: param_loading_2D;
		cargo_capacity: sw_hankyu6300_capacity_main()*param_capacity_mod/3;  
		can_attach_wagon: sw_1435_attach_vehid;
		// cargo_subtype_text:sw_hankyu6300_lv_desc;
		additional_text:  string(STR_CONCAT_8,
											string(STR_STANDARDGAUGE),
											string(STR_LOAD_SPEED,
											string(STR_2_DOOR),),
											string(STR_FORMATION),
											string(STR_hankyu6300_DESC),
											string(STR_CARS),
											string(STR_COUPLER,
											string(STR_SHIBATA)),
											string(STR_USAGE,
											string(STR_EXPRESS),
											string(STR_DC),
											),
											string(STR_OPERATOR,
											string(STR_HANKYU)));       
    }
    livery_override(mu_car){
			loading_speed: param_loading_2D;
			cargo_capacity: sw_hankyu6300_capacity_main()*param_capacity_mod/3;
			default: sw_hankyu6300_lv;
			power: sw_hankyu6300_power_main()*4*1342/1000;
			weight: 31;
		}
}