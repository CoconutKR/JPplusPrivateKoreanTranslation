//livery 1
spriteset(sotetsu20000_front_base, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu20000/sotetsu20000.png"){ tmpl_std(0, 0) }
spriteset(sotetsu20000_end_base, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu20000/sotetsu20000.png"){ tmpl_std_rev(0, 0) }
spriteset(sotetsu20000_car_base, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu12000/sotetsu12000.png"){ tmpl_symm(0, 25) }
spriteset(sotetsu20000_lights_rear, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu20000/sotetsu20000_lights.png"){ tmpl_high_skirt(0, 0) }
spriteset(sotetsu20000_lights_back, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu20000/sotetsu20000_lights.png"){ tmpl_end_v2(0, 25) }

TRAIN_CONSTRUCTOR_4(sotetsu20000_front, sotetsu20000_front_base, roof_gray_med_h1, sotetsu20000_lights_rear, ac_long_toshiba)
TRAIN_CONSTRUCTOR_4(sotetsu20000_end, sotetsu20000_end_base, roof_gray_med_h1_rev, sotetsu20000_lights_back, ac_long_toshiba)
TRAIN_CONSTRUCTOR_3(sotetsu20000_wag, sotetsu20000_car_base, roof_gray_med, ac_long_toshiba)
TRAIN_CONSTRUCTOR_4(sotetsu20000_mot, sotetsu20000_car_base, roof_gray_med, ac_long_toshiba, panto_arm_n_single)
TRAIN_CONSTRUCTOR_4(sotetsu20000_mot_rev, sotetsu20000_car_base, roof_gray_med, ac_long_toshiba, panto_arm_n_single_rev)

//*purchase
  spriteset(sotetsu20000_front_base_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu20000/sotetsu20000.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(sotetsu20000_car_base_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/sotetsu/sotetsu12000/sotetsu12000.png"){ tmpl_purchase_2c(0, 25) }
  GRAPHIC_STACK_7(sotetsu20000_purchase, logo_sotetsu, roof_gray_med_h1_c1, sotetsu20000_front_base_c1, ac_long_toshiba_c1, roof_gray_med_c2, sotetsu20000_car_base_c2, ac_long_toshiba_c2)

//*livery 1
	//panto placer
	//1 10
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_panto_placer_1,position_in_vehid_chain %5){
		0: sotetsu20000_mot;
		2: sotetsu20000_mot;
		3: sotetsu20000_mot;
		sotetsu20000_wag;
	}
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_panto_placer_1_rev,position_in_vehid_chain_from_end %5 -1){
		0: sotetsu20000_mot_rev;
		2: sotetsu20000_mot_rev;
		3: sotetsu20000_mot_rev;
		sotetsu20000_wag;
	}
	switch(FEAT_TRAINS, PARENT, sw_sotetsu20000_panto_placer_main,vehicle_is_reversed){
		1: sw_sotetsu20000_panto_placer_1_rev;
		sw_sotetsu20000_panto_placer_1;
	}
	//front-end-fixer
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_end_gfx_main,position_in_vehid_chain_from_end){
		0: sotetsu20000_end;
		default: sw_sotetsu20000_panto_placer_main;
	}
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_front_gfx_main,position_in_vehid_chain_from_end){
		0: sotetsu20000_front;
		default: sw_sotetsu20000_panto_placer_main;
	}
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_lv,vehicle_type_id){
		mu_car_narrow:sw_sotetsu20000_end_gfx_main;
		sw_sotetsu20000_front_gfx_main;
	}
//
//post fixers
//capacity-changer
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_capacity,position_in_vehid_chain_from_end){
    0: 133;
    default: 144;
	}
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_capacity_1,position_in_vehid_chain){
    0: 133;
    default: sw_sotetsu20000_capacity;
	}
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_capacity_main,vehicle_type_id){
    mu_car_narrow:sw_sotetsu20000_capacity;
    sw_sotetsu20000_capacity_1;
	}
//power fix
	switch(FEAT_TRAINS, SELF, sw_sotetsu20000_power_main,position_in_vehid_chain_from_end%10){
    1:return 190;
    3..4:return 190;
    6:return 190;
    8:return 190;
    return 0;
	}
//
item(FEAT_TRAINS, sotetsu20000) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_sotetsu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_TRAIN_NAME, string(STR_NAME_SOTETSU), "20000/21000", string(STR_NAME_SERIES),);
        introduction_date: date(2018, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 35;
        reliability_decay: 10;

        cost_factor: 20;
        running_cost_factor: 50;
        speed: 120 km/h;
        track_type: TR_1067_DC;
        power: 1 kW;
        weight: 34 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_sotetsu20000_lv;
        power:sw_sotetsu20000_power_main()*4*1342/1000;
        start_stop: sw_stop_start_3;
        purchase: sotetsu20000_purchase;
        can_attach_wagon: sw_1067_attach_vehid;
        //cargo_subtype_text:sw_sotetsu20000_lv_desc;
        cargo_capacity: sw_sotetsu20000_capacity_main()*param_capacity_mod/3;  
        additional_text:string(STR_TRAIN_DESC,
                        string(STR_NARROWGAUGE),
                        string(STR_DENKI, string(STR_DC_N, 1500)),
                        string(STR_LOAD_SPEED, string(STR_DOORS, 4),),
                        string(STR_FORMATION, string(STR_NUM_CARS_2, 8,10)),
                        string(STR_USAGE, string(STR_METRO_ACCESS)),
                        string(STR_OPERATOR, string(STR_SOTETSU)));
    }
    livery_override(mu_car_narrow){
        default: sw_sotetsu20000_lv;
        loading_speed: param_loading_4D;
        power:  sw_sotetsu20000_power_main()*4*1342/1000;
        cargo_capacity: sw_sotetsu20000_capacity_main()*param_capacity_mod/3;
        weight: 32;
      }
}