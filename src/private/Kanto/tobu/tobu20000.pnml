spriteset (tobu20000_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tobu20000_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000_purchase.png"){ tmpl_purchase(0, 0) }

//livery 1
spriteset (tobu20000_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_front_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_end_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_mot_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_front_f,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tobu20000_end_f,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }

alternative_sprites(tobu20000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std(0, 0) }
alternative_sprites(tobu20000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tobu20000_front_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std(0, 50) }
alternative_sprites(tobu20000_end_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std_rev(0, 75) }
alternative_sprites(tobu20000_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std(0, 100) }
alternative_sprites(tobu20000_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std_rev(0, 100) }
alternative_sprites(tobu20000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_symm(0, 125) }
alternative_sprites(tobu20000_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std_rev(0, 150) }
alternative_sprites(tobu20000_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu20000/tobu20000.png"){ tmpl_std(0, 150) }

//*livery 1
//special gfx
//front gfx
switch(FEAT_TRAINS, PARENT, sw_tobu20000_front_gfx_0, vehicle_is_reversed){
    1:tobu20000_front;
    tobu20000_front_mot;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_front_gfx, num_vehs_in_consist){
    2:sw_tobu20000_front_gfx_0;
    tobu20000_front;
}
//end gfx
switch(FEAT_TRAINS, PARENT, sw_tobu20000_end_gfx_0, vehicle_is_reversed){
    1:tobu20000_end_mot;
    tobu20000_end;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_end_gfx, num_vehs_in_consist){
    2:sw_tobu20000_end_gfx_0;
    tobu20000_end;
}

//panto placer
//1
switch(FEAT_TRAINS, SELF, sw_tobu20000_panto_placer_1,position_in_vehid_chain %2){
    0: tobu20000_mot;
    tobu20000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_panto_placer_1_rev,1-position_in_vehid_chain_from_end %2){
    0: tobu20000_mot_rev;
    tobu20000_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tobu20000_panto_placer_main,vehicle_is_reversed){
    1: sw_tobu20000_panto_placer_1_rev;
    sw_tobu20000_panto_placer_1;
}

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_tobu20000_fake_placer,position_in_vehid_chain +1){
    5: tobu20000_front_f;
    6: tobu20000_end_f;
sw_tobu20000_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_fake_placer_rev,position_in_vehid_chain_from_end ){
    5: tobu20000_end_f;
    6: tobu20000_front_f;
sw_tobu20000_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_tobu20000_fake_placer_gfx,vehicle_is_reversed){
    1: sw_tobu20000_fake_placer_rev;
    sw_tobu20000_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_fake_placer_main,num_vehs_in_vehid_chain>10 - 1){
    1: sw_tobu20000_fake_placer_gfx;
sw_tobu20000_panto_placer_main;
}

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tobu20000_end_gfx_main,position_in_vehid_chain_from_end){
    0: sw_tobu20000_end_gfx;
    default: sw_tobu20000_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_front_gfx_main,position_in_vehid_chain_from_end){
    0: sw_tobu20000_front_gfx;
    default: sw_tobu20000_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_lv,vehicle_type_id){
    mu_car_narrow:sw_tobu20000_end_gfx_main;
    sw_tobu20000_front_gfx_main;
}

//post fixers
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tobu20000_capacity,position_in_vehid_chain_from_end){
    0: 120;
    default: 130;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_capacity_1,position_in_vehid_chain){
    0: 120;
    default: sw_tobu20000_capacity;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_capacity_main,vehicle_type_id){
    mu_car_narrow:sw_tobu20000_capacity;
    sw_tobu20000_capacity_1;
}
//power fix
//1992
switch(FEAT_TRAINS, SELF, sw_tobu20000_power_0,build_year){
    0..1991 :return 140;
    return 150;
}

switch(FEAT_TRAINS, SELF, sw_tobu20000_power,position_in_vehid_chain_from_end%8){
    1..6:sw_tobu20000_power_0;
    return 0;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_head_power,num_vehs_in_consist){
    2: sw_tobu20000_power_0;
    return 0;
}
switch(FEAT_TRAINS, SELF, sw_tobu20000_power_main,vehicle_type_id){
    mu_car_narrow:sw_tobu20000_power;
    sw_tobu20000_head_power;
}

//attachable vehicles
switch(FEAT_TRAINS, SELF, tobu20000_attach_vehid, vehicle_type_id) {
  mu_car: return string(STR_ATTACH_1067_ERROR);
  mu_car_metro: return string(STR_ATTACH_1067_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tobu20000,2075) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_tobu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_tobu20000_NAME);
        introduction_date: date(1988, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 35;
        reliability_decay: 20;

        cost_factor: 17;
        running_cost_factor: 47;
        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 1 kW;
        weight: 33 ton;
        loading_speed: param_loading_3D;
    }
	graphics {
        power:sw_tobu20000_power_main()*4*1342/1000;
        start_stop: sw_stop_start_2;
        default: sw_tobu20000_lv;
        purchase: tobu20000_purchase;
        can_attach_wagon: tobu20000_attach_vehid;
        //cargo_subtype_text:sw_tobu20000_lv_desc;
        additional_text:    string(STR_CONCAT_8,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_3_DOOR),),
                            string(STR_FORMATION),
                            string(STR_8_CARS),
                            string(STR_CARS),
                            string(STR_COUPLER,
                            string(STR_KNUCKLE)),
                            string(STR_USAGE,
                            string(STR_COMMUTER),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR,
                            string(STR_TOBU),
                            ),
                            
                        );
                         
        cargo_capacity: sw_tobu20000_capacity_main()*param_capacity_mod/3;  
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_tobu20000_capacity_main()*param_capacity_mod/3;
        default:                      sw_tobu20000_lv;
        power:  sw_tobu20000_power_main()*4*1342/1000;
        weight: 33;
      }
}