//purchase
  spriteset(tobu500_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(tobu500_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_purchase_2c(88, 50) }
  GRAPHIC_STACK_3(tobu500_purchase, logo_tobu, tobu500_purchase_c1, tobu500_purchase_c2)
//livery 1
    spriteset(tobu500_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std(0, 0) }
    spriteset(tobu500_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std_rev(0, 25) }
    spriteset(tobu500_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std(0, 50) }
    spriteset(tobu500_wag_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std_rev(0, 50) }
    spriteset(tobu500_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std(0, 75) }
    spriteset(tobu500_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std_rev(0, 75) }
    spriteset(tobu500_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std_rev(0, 100) }
    spriteset(tobu500_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu500/tobu500.png"){ tmpl_std(0, 100) }

//*livery 1
  //panto placer
    //var 1
    switch(FEAT_TRAINS, SELF, sw_tobu500_panto_placer_1,position_in_vehid_chain %3){
        1: tobu500_mot;
        tobu500_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_panto_placer_1_rev,position_in_vehid_chain_from_end %3-1){
        1: tobu500_mot_rev;
        tobu500_wag_rev;
    }
    switch(FEAT_TRAINS, PARENT, sw_tobu500_panto_placer_main_1,vehicle_is_reversed){
        1: sw_tobu500_panto_placer_1_rev;
        sw_tobu500_panto_placer_1;
    }
    //var 2
    switch(FEAT_TRAINS, PARENT, sw_tobu500_panto_placer_main_2,vehicle_is_reversed){
        1: tobu500_wag_rev;
        tobu500_wag;
    }
    //main
    switch(FEAT_TRAINS, SELF, sw_tobu500_panto_placer_main,num_vehs_in_vehid_chain+1){
        0..4:sw_tobu500_panto_placer_main_2;
        7:sw_tobu500_panto_placer_main_2;
        sw_tobu500_panto_placer_main_1;
    }

  //fake head-end placer
    switch(FEAT_TRAINS, SELF, sw_tobu500_fake_placer,position_in_vehid_chain +1){
        2: tobu500_front_f;
        3: tobu500_end_f;
    sw_tobu500_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_fake_placer_rev,position_in_vehid_chain_from_end ){
        2: tobu500_end_f;
        3: tobu500_front_f;
    sw_tobu500_panto_placer_main;
    }
    switch(FEAT_TRAINS, PARENT, sw_tobu500_fake_placer_gfx,vehicle_is_reversed){
        1: sw_tobu500_fake_placer_rev;
        sw_tobu500_fake_placer;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_fake_placer_main,num_vehs_in_vehid_chain>5 - 1){
        1: sw_tobu500_fake_placer_gfx;
    sw_tobu500_panto_placer_main;
    }

  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tobu500_end_gfx_main,position_in_vehid_chain_from_end){
        0: tobu500_end;
        default: sw_tobu500_fake_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_front_gfx_main,position_in_vehid_chain_from_end){
        0: tobu500_front;
        default: sw_tobu500_fake_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_lv,vehicle_type_id){
        mu_car_narrow:sw_tobu500_end_gfx_main;
        sw_tobu500_front_gfx_main;
    }

//capacity-changer
    switch(FEAT_TRAINS, SELF, sw_tobu500_capacity_1_0,position_in_vehid_chain){
        1: 112;
        2: 112;
        default: 136;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_capacity_1,position_in_vehid_chain_from_end){
        0: 112;
        default: sw_tobu500_capacity_1_0;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_capacity_2,position_in_vehid_chain_from_end){
        0: 112;
        default: 136;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_capacity,num_vehs_in_vehid_chain>4){
        1: sw_tobu500_capacity_1;
        default: sw_tobu500_capacity_2;
    }

    //power fix
    switch(FEAT_TRAINS, SELF, sw_tobu500_power,position_in_vehid_chain_from_end%6){
        0:return 760;
        2:return 760;
        3:return 760;
        return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu500_power_main,vehicle_type_id){
        mu_car_narrow:sw_tobu500_power;
        return 760;
    }

  //attachable vehicles
    switch(FEAT_TRAINS, SELF, tobu500_attach_vehid, vehicle_type_id) {
    mu_car_narrow:  return CB_RESULT_ATTACH_ALLOW;
    return string(STR_ATTACH_ERROR);
    }

item(FEAT_TRAINS, tobu500,2061) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_tobu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_TRAIN_NAME_LONG, string(STR_NAME_TOBU), "500", string(STR_NAME_SERIES), string(STR_NAME_TOBU_REVATY));
        introduction_date: date(2016, 1, 1);
        model_life: 17;
        vehicle_life: 22;
        reliability_decay: 10;

        cost_factor: 21;
        running_cost_factor: 51;
        speed: 130 km/h;
        track_type: TR_1067_DC;
        power: 520 kW;
        weight: 39 ton;
        loading_speed: param_loading_1D;
    }
	graphics {
        default: sw_tobu500_lv;
        purchase: tobu500_purchase;
        cargo_age_period:ltd_express_cargo_age; 
        can_attach_wagon: tobu500_attach_vehid;
        //cargo_subtype_text:sw_tobu500_lv_desc;
        additional_text:
		string(STR_TRAIN_DESC,
		string(STR_NARROWGAUGE),
		string(STR_DENKI, string(STR_DC_N, 1500)),
		string(STR_LOAD_SPEED, string(STR_DOORS, 1)),
		string(STR_FORMATION, string(STR_NUM_CARS_2, 3,6)),
		string(STR_USAGE, string(STR_LIMITED)),
		string(STR_OPERATOR, string(STR_TOBU)));

		cargo_capacity: 112*param_capacity_mod/3;
        power: sw_tobu500_power_main()*1342/1000;  
    }
    livery_override(mu_car_narrow){
        cargo_age_period:ltd_express_cargo_age; 
        loading_speed: param_loading_1D;
        cargo_capacity: sw_tobu500_capacity()*param_capacity_mod/3;
        default:                      sw_tobu500_lv;
        power: sw_tobu500_power_main()*1342/1000;
        weight: 38;
      }
}