//purchase
  spriteset(tobu6000_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(tobu6000_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_purchase_2c(0, 75) }
  GRAPHIC_STACK_3(tobu6000_purchase, logo_tobu, tobu6000_purchase_c1, tobu6000_purchase_c2)
//livery 1
    spriteset(tobu6000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_std(0, 0) }
    spriteset(tobu6000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_std_rev(0, 25) }
    spriteset(tobu6000_end_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_std(0, 25) }
    spriteset(tobu6000_front_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_std(0, 50) }
    spriteset(tobu6000_end_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_std_rev(0, 75) }
    spriteset(tobu6000_end_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tobu/tobu6000/tobu6000.png"){ tmpl_std(0, 75) }

//*livery 1
  //special gfx
    //front&end
    switch(FEAT_TRAINS, PARENT, sw_tobu6000_front_gfx, vehicle_is_reversed){
        1: tobu6000_front;
        tobu6000_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_tobu6000_end_gfx, vehicle_is_reversed){
        1: tobu6000_end_mot;
        tobu6000_end;
    }

  //panto placer
    switch(FEAT_TRAINS, SELF, sw_tobu6000_panto_placer,position_in_vehid_chain %2){
        0:tobu6000_end;
        tobu6000_end_mot_rev;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu6000_panto_placer_rev,1-position_in_vehid_chain_from_end %2){
        0: tobu6000_end_rev;
        tobu6000_end_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_tobu6000_panto_placer_main,vehicle_is_reversed){
        1: sw_tobu6000_panto_placer_rev;
        sw_tobu6000_panto_placer;
    }
  //end car fixer
    switch(FEAT_TRAINS, SELF, sw_tobu6000_end_fix, position_in_vehid_chain_from_end){
        0:sw_tobu6000_end_gfx;
        sw_tobu6000_panto_placer_main;
    }

    switch(FEAT_TRAINS, SELF, sw_tobu6000_livery_1,vehicle_type_id){
        100..102:sw_tobu6000_end_fix;
        sw_tobu6000_front_gfx;
    }
//power fixer
    switch(FEAT_TRAINS, SELF, tobu6000_power, position_in_vehid_chain %2) {
        1: return 130*4*1342/1000;
    return 0;
    }
//capacity-changer
    switch(FEAT_TRAINS, SELF, sw_tobu6000_capacity_1_0,position_in_vehid_chain %2){
        0: 145;
        default: 150;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu6000_capacity_1,vehicle_type_id){
        100..102:sw_tobu6000_capacity_1_0;
        150;
    }
    //limited service
    switch(FEAT_TRAINS, SELF, sw_tobu6000_capacity_2_0,position_in_vehid_chain %2){
        0: 81;
        default: 84;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu6000_capacity_2,vehicle_type_id){
        100..102:sw_tobu6000_capacity_2_0;
        84;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu6000_capacity,cargo_subtype){
        0:sw_tobu6000_capacity_2;
        1:sw_tobu6000_capacity_1;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu6000_cargo_age_main,cargo_subtype){
        0:ltd_express_cargo_age;
        1:express_cargo_age;
    }
//*livery controller
    switch(FEAT_TRAINS, PARENT, sw_tobu6000_lv, cargo_subtype){
        0:sw_tobu6000_livery_1;
        1:sw_tobu6000_livery_1;
    }
    switch(FEAT_TRAINS, SELF, sw_tobu6000_lv_desc, cargo_subtype){
        0:string(STR_CONCAT_2,
        string(LV_DEFAULT),
        string(STR_PAX_LTD_AGE));
        1:string(STR_CONCAT_2,
        string(LV_DEFAULT),
        string(STR_PAX_AGE));
    }
item(FEAT_TRAINS, tobu6000,2053) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_tobu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_TRAIN_NAME, string(STR_NAME_TOBU), "6000", string(STR_NAME_SERIES));
        introduction_date: date(1964, 1, 1);
        vehicle_life: 22;
        model_life: 17;
        reliability_decay: 10;

        cost_factor: 16;
        running_cost_factor: 46;
        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 520 kW;
        weight: 36 ton;
        loading_speed: param_loading_2D;
    }
	graphics {
		start_stop: sw_stop_start_2;
        default: sw_tobu6000_lv;
		purchase: tobu6000_purchase;
        cargo_age_period:sw_tobu6000_cargo_age_main; 
		can_attach_wagon: sw_1067_attach_vehid;
		cargo_subtype_text:sw_tobu6000_lv_desc;
		additional_text:
				string(STR_TRAIN_DESC,
				string(STR_NARROWGAUGE),
				string(STR_DENKI, string(STR_DC_N, 1500)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 2)),
				string(STR_FORMATION, string(STR_NUM_CARS_3, 2,4,6)),
	    string(STR_USAGE,
				string(STR_CONCAT_2_OR, string(STR_EXPRESS), string(STR_LIMITED))),
				string(STR_OPERATOR, string(STR_TOBU)));

		cargo_capacity: sw_tobu6000_capacity()*param_capacity_mod/3;
    }
	livery_override(mu_car_narrow){
		cargo_subtype_text:sw_tobu6000_lv_desc;
        cargo_age_period:sw_tobu6000_cargo_age_main; 
        loading_speed: param_loading_2D;
        cargo_capacity: sw_tobu6000_capacity()*param_capacity_mod/3;
        default: sw_tobu6000_lv;
        power: tobu6000_power;
        weight: 35;
      }
}