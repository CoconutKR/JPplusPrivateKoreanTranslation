//set sprites
spriteset (tokyu3000_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tokyu3000_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (tokyu3000_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu3000_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 0) }
spriteset (tokyu3000_end_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 0) }
spriteset (tokyu3000_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu3000_mot_1,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu3000_mot_1_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu3000_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }

alternative_sprites(tokyu3000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_std(0, 0) }
alternative_sprites(tokyu3000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tokyu3000_end_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_std(0, 25) }
alternative_sprites(tokyu3000_mot_1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_std(0, 50) }
alternative_sprites(tokyu3000_mot_1_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(tokyu3000_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_symm(0, 75) }
alternative_sprites(tokyu3000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu3000/tokyu3000.png"){ tmpl_symm(0, 100) }

//*livery 1
//panto placer
//var 1
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_1,1+position_in_vehid_chain %7){
    2: tokyu3000_mot;
    4: tokyu3000_mot_1;
    6: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_1_rev,position_in_vehid_chain_from_end %7){
    2: tokyu3000_mot;
    4: tokyu3000_mot_1_rev;
    6: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu3000_panto_placer_main_1,vehicle_is_reversed){
    1: sw_tokyu3000_panto_placer_1_rev;
    sw_tokyu3000_panto_placer_1;
}

//var 2
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_2,1+position_in_vehid_chain %5){
    2: tokyu3000_mot;
    4: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_2_rev,position_in_vehid_chain_from_end %5){
    2: tokyu3000_mot;
    4: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu3000_panto_placer_main_2,vehicle_is_reversed){
    1: sw_tokyu3000_panto_placer_2_rev;
    sw_tokyu3000_panto_placer_2;
}

//var 3
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_3,1+position_in_vehid_chain %4){
    2: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_3_rev,position_in_vehid_chain_from_end %4){
    2: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu3000_panto_placer_main_3,vehicle_is_reversed){
    1: sw_tokyu3000_panto_placer_3_rev;
    sw_tokyu3000_panto_placer_3;
}

//var 4
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_4,1+position_in_vehid_chain %3){
    1: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_4_rev,position_in_vehid_chain_from_end %3){
    1: tokyu3000_mot;
    tokyu3000_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu3000_panto_placer_main_4,vehicle_is_reversed){
    1: sw_tokyu3000_panto_placer_4_rev;
    sw_tokyu3000_panto_placer_4;
}
switch(FEAT_TRAINS,PARENT,sw_tokyu3000_panto_placer_time,(build_year<2000||date_of_last_service<2000)){
    1:sw_tokyu3000_panto_placer_main_3;
    sw_tokyu3000_panto_placer_main_1;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_panto_placer_main,num_vehs_in_vehid_chain+1){
    0..3:sw_tokyu3000_panto_placer_main_4;
    4..7: sw_tokyu3000_panto_placer_main_2;
    sw_tokyu3000_panto_placer_time;
}

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_tokyu3000_fake_placer,position_in_vehid_chain +1){
    7: tokyu3000_end;
    8: tokyu3000_end_rev;
sw_tokyu3000_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_fake_placer_rev,position_in_vehid_chain_from_end ){
    7: tokyu3000_end;
    8: tokyu3000_end_rev;
sw_tokyu3000_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu3000_fake_placer_gfx,vehicle_is_reversed){
    1: sw_tokyu3000_fake_placer_rev;
    sw_tokyu3000_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_fake_placer_main,num_vehs_in_vehid_chain>11 - 1){
    1: sw_tokyu3000_fake_placer_gfx;
sw_tokyu3000_panto_placer_main;
}

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tokyu3000_end_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu3000_end;
    default: sw_tokyu3000_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_front_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu3000_front;
    default: sw_tokyu3000_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_lv,vehicle_type_id){
    mu_car_narrow:sw_tokyu3000_end_gfx_main;
    sw_tokyu3000_front_gfx_main;
}

//post fixers
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tokyu3000_capacity,position_in_vehid_chain_from_end){
    0: 141*param_capacity_mod/3;
    default: 151*param_capacity_mod/3;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_capacity_1,position_in_vehid_chain){
    0: 141*param_capacity_mod/3;
    default: sw_tokyu3000_capacity;
}
switch(FEAT_TRAINS, SELF, sw_tokyu3000_capacity_main,vehicle_type_id){
    mu_car_narrow:sw_tokyu3000_capacity;
    mu_car:sw_tokyu3000_capacity;
    sw_tokyu3000_capacity_1;
}

//TODO: attachable vehicles -- will be changed later
switch(FEAT_TRAINS, SELF, tokyu3000_attach_vehid, vehicle_type_id) {
  mu_car: return string(STR_ATTACH_1067_ERROR);
  mu_car_metro: return string(STR_ATTACH_1067_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tokyu3000,2017) {
    property {
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        misc_flags: bitmask(TRAIN_FLAG_MU);
        sprite_id: SPRITE_ID_NEW_TRAIN;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        tractive_effort_coefficient: 0.3;
        cargo_capacity: 130;
        air_drag_coefficient: 0.07;
        ai_special_flag: AI_FLAG_PASSENGER;

        climates_available: param_disable_tokyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        
        name: string(STR_NAME_DENSHA, string(STR_NAME_TOKYU), "3000", string(STR_NAME_SERIES));
        introduction_date: date(1999, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 20;

        cost_factor: 22;
        running_cost_factor: 52;
        speed: 120 km/h;
        track_type: TR_1067_DC;
        power: 760 kW;
        weight: 32 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_tokyu3000_lv;
        purchase: tokyu3000_purchase;
        can_attach_wagon: tokyu3000_attach_vehid;
        start_stop: sw_stop_start_3; 
        cargo_capacity: sw_tokyu3000_capacity_main; 
        //cargo_subtype_text: sw_tokyu3000_lv_desc;
        additional_text:    string(STR_CONCAT_8,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_DOORS, 4),),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_2, 6,8),
                            // string(STR_COUPLER,
                            // string(STR_KNUCKLE)),
                            string(STR_USAGE,
                            string(STR_COMMUTER),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR,
                            string(STR_TOKYU),
                            ),
                            
                        );
         
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tokyu3000_capacity_main;
        default: sw_tokyu3000_lv;
        power:1019*3/8;
        weight: 30;
      }
}