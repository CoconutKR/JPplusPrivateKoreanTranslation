//set sprites
spriteset (tokyu6000_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tokyu6000_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000_purchase.png"){ tmpl_purchase(0, 0) }
//tokyu6000
spriteset (tokyu6000_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 0) }
spriteset (tokyu6000_mot_1,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot_1_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot_2,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot_2_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_wag_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot_1_Qseat,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tokyu6000_mot_1_rev_Qseat,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }

alternative_sprites(tokyu6000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std(0, 0) }
alternative_sprites(tokyu6000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tokyu6000_mot_1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std(0, 50) }
alternative_sprites(tokyu6000_mot_1_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(tokyu6000_mot_2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std(0, 75) }
alternative_sprites(tokyu6000_mot_2_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std_rev(0, 75) }
alternative_sprites(tokyu6000_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std(0, 100) }
alternative_sprites(tokyu6000_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std_rev(0, 100) }
alternative_sprites(tokyu6000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std(0, 125) }
alternative_sprites(tokyu6000_wag_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std_rev(0, 125) }
alternative_sprites(tokyu6000_mot_1_Qseat, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std(0, 150) }
alternative_sprites(tokyu6000_mot_1_rev_Qseat, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000/tokyu6000.png"){ tmpl_std_rev(0, 150) }

//*livery 1
//special gfx
//wagon gfx
switch(FEAT_TRAINS, SELF, sw_tokyu6000_wag_gfx_1, 1+position_in_vehid_chain>(num_vehs_in_vehid_chain/2)){
    1:tokyu6000_wag_rev;
    tokyu6000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_wag_gfx_2, position_in_vehid_chain_from_end>(num_vehs_in_vehid_chain/2)){
    1:tokyu6000_wag;
    tokyu6000_wag_rev;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_wag_gfx, vehicle_is_reversed){
    1:sw_tokyu6000_wag_gfx_1;
    sw_tokyu6000_wag_gfx_2;
}

//motor car gfx
switch(FEAT_TRAINS, SELF, sw_tokyu6000_mot_gfx_1, 1+position_in_vehid_chain>(num_vehs_in_vehid_chain/2)){
    1:tokyu6000_mot_rev;
    tokyu6000_mot;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_mot_gfx_2, position_in_vehid_chain_from_end>(num_vehs_in_vehid_chain/2)){
    1:tokyu6000_mot;
    tokyu6000_mot_rev;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_mot_gfx, vehicle_is_reversed){
    1:sw_tokyu6000_mot_gfx_2;
    sw_tokyu6000_mot_gfx_1;
}

//motor_1 car gfx
switch(FEAT_TRAINS, SELF, sw_tokyu6000_mot_1_gfx_1, 1+position_in_vehid_chain>(num_vehs_in_vehid_chain/2)){
    1:tokyu6000_mot_1_rev;
    tokyu6000_mot_2;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_mot_1_rev_gfx_1, position_in_vehid_chain_from_end>(num_vehs_in_vehid_chain/2)){
    1:tokyu6000_mot_1;
    tokyu6000_mot_2_rev;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_mot_1_gfx, vehicle_is_reversed){
    1:sw_tokyu6000_mot_1_rev_gfx_1;
    sw_tokyu6000_mot_1_gfx_1;
}

//panto placer
//var 1 // 6cars
switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_1,1+position_in_vehid_chain %5){
    1: sw_tokyu6000_mot_1_gfx;
    4: sw_tokyu6000_mot_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_1_rev,position_in_vehid_chain_from_end %5){
    1: sw_tokyu6000_mot_1_gfx;
    4: sw_tokyu6000_mot_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_panto_placer_main_1,vehicle_is_reversed){
    1: sw_tokyu6000_panto_placer_1_rev;
    sw_tokyu6000_panto_placer_1;
}

//var 2 // default
switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_2,1+position_in_vehid_chain %6){
    1: sw_tokyu6000_mot_1_gfx;
    2: sw_tokyu6000_mot_1_gfx;
    5: sw_tokyu6000_mot_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_2_rev,position_in_vehid_chain_from_end %6){
    1: sw_tokyu6000_mot_1_gfx;
    2: sw_tokyu6000_mot_1_gfx;
    5: sw_tokyu6000_mot_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_panto_placer_main_2,vehicle_is_reversed){
    1: sw_tokyu6000_panto_placer_2_rev;
    sw_tokyu6000_panto_placer_2;
}

//var 3. For short forms
switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_3,1+position_in_vehid_chain %4){
    1: sw_tokyu6000_mot_gfx;
    3: sw_tokyu6000_mot_1_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_3_rev,position_in_vehid_chain_from_end %4){
    1: sw_tokyu6000_mot_gfx;
    3: sw_tokyu6000_mot_1_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_panto_placer_main_3,vehicle_is_reversed){
    1: sw_tokyu6000_panto_placer_3_rev;
    sw_tokyu6000_panto_placer_3;
}

switch(FEAT_TRAINS, SELF, sw_tokyu6000_panto_placer_main,num_vehs_in_vehid_chain+1){
    0..5:sw_tokyu6000_panto_placer_main_3;
    6: sw_tokyu6000_panto_placer_main_1;
    sw_tokyu6000_panto_placer_main_2;
}

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tokyu6000_end_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu6000_end;
    default: sw_tokyu6000_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_front_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu6000_front;
    default: sw_tokyu6000_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_livery_1,vehicle_type_id){
    mu_car_narrow:sw_tokyu6000_end_gfx_main;
    sw_tokyu6000_front_gfx_main;
}

//*livery 2
//special gfx
//Q-car
switch(FEAT_TRAINS, SELF, sw_tokyu6000_1_mot_1_Qcar_gfx_1,position_in_vehid_chain+1){
    2: tokyu6000_mot_1_rev_Qseat;
    sw_tokyu6000_mot_1_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_1_mot_1_Qcar_gfx_2,position_in_vehid_chain_from_end){
    2: tokyu6000_mot_1_Qseat;
    sw_tokyu6000_mot_1_gfx;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_1_mot_1_Qcar_gfx,vehicle_is_reversed){
    1: sw_tokyu6000_1_mot_1_Qcar_gfx_2;
    sw_tokyu6000_1_mot_1_Qcar_gfx_1;
}

//panto placer
//var 2 // default
switch(FEAT_TRAINS, SELF, sw_tokyu6000_1_panto_placer_2,1+position_in_vehid_chain %6){
    1: sw_tokyu6000_mot_1_gfx;
    2: sw_tokyu6000_1_mot_1_Qcar_gfx;
    5: sw_tokyu6000_mot_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_1_panto_placer_2_rev,position_in_vehid_chain_from_end %6){
    1: sw_tokyu6000_mot_1_gfx;
    2: sw_tokyu6000_1_mot_1_Qcar_gfx;
    5: sw_tokyu6000_mot_gfx;
    sw_tokyu6000_wag_gfx;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_1_panto_placer_main_2,vehicle_is_reversed){
    1: sw_tokyu6000_1_panto_placer_2_rev;
    sw_tokyu6000_1_panto_placer_2;
}

switch(FEAT_TRAINS, SELF, sw_tokyu6000_1_panto_placer_main,num_vehs_in_vehid_chain+1){
    0..5:sw_tokyu6000_panto_placer_main_3;
    6: sw_tokyu6000_panto_placer_main_1;
    sw_tokyu6000_1_panto_placer_main_2;
}

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tokyu6000_1_end_gfx_main,position_in_vehid_chain_from_end){
    0: tokyu6000_end;
    default: sw_tokyu6000_1_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_livery_2,vehicle_type_id){
    mu_car_narrow:sw_tokyu6000_1_end_gfx_main;
    sw_tokyu6000_front_gfx_main;
}

//livery controller
random_switch(FEAT_TRAINS, PARENT, sw_tokyu6000_livery_0){
    2:sw_tokyu6000_livery_1;
    1:sw_tokyu6000_livery_2;
}

switch(FEAT_TRAINS, PARENT, sw_tokyu6000_lv,cargo_subtype){
    0:sw_tokyu6000_livery_0;
    1:sw_tokyu6000_livery_1;
    2:sw_tokyu6000_livery_2;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_lv_desc,cargo_subtype){
    0:string(LV_RANDOM);
    1:string(LV_DEFAULT);
    2:string(LV_tokyu6000_2);
}

//post fixers
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tokyu6000_capacity,position_in_vehid_chain_from_end){
    0: 141*param_capacity_mod/3;
    default: 149*param_capacity_mod/3;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_capacity_1,position_in_vehid_chain){
    0: 141*param_capacity_mod/3;
    default: sw_tokyu6000_capacity;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_capacity_main,vehicle_type_id){
    mu_car_narrow:sw_tokyu6000_capacity;
    sw_tokyu6000_capacity_1;
}

//TODO attachable vehicles -- will be changed later
switch(FEAT_TRAINS, SELF, tokyu6000_attach_vehid, vehicle_type_id) {
  mu_car: return string(STR_ATTACH_1067_ERROR);
  mu_car_metro: return string(STR_ATTACH_1067_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}
item(FEAT_TRAINS, tokyu6000,2021) {
    property {
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        misc_flags: bitmask(TRAIN_FLAG_MU);
        sprite_id: SPRITE_ID_NEW_TRAIN;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        tractive_effort_coefficient: 0.3;
        cargo_capacity: 130;
        air_drag_coefficient: 0.07;
        ai_special_flag: AI_FLAG_PASSENGER;

        climates_available: param_disable_tokyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        //variant_group:;
        
        name: string(STR_NAME_DENSHA, string(STR_NAME_TOKYU), "6000", string(STR_NAME_SERIES));
        introduction_date: date(2008, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 20;

        cost_factor: 23;
        running_cost_factor: 52;
        speed: 120 km/h;
        track_type: TR_1067_DC;
        power: 760 kW;
        weight: 28 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_tokyu6000_lv;
        purchase: tokyu6000_purchase;
        can_attach_wagon: tokyu6000_attach_vehid;
        start_stop: sw_stop_start_3; 
        cargo_capacity: sw_tokyu6000_capacity_main; 
        cargo_subtype_text: sw_tokyu6000_lv_desc;
        additional_text:    string(STR_CONCAT_8,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_DOORS, 4),),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_2, 6,7),
                            // string(STR_COUPLER,
                            // string(STR_KNUCKLE)),
                            string(STR_USAGE,
                            string(STR_COMMUTER),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR,
                            string(STR_TOKYU),
                            ),
                            
                        );
         
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tokyu6000_capacity_main;
        default: sw_tokyu6000_lv;
        power:1019*3/7;
        weight: 30;
      }
}