//purchase
  spriteset(tokyu6000_old_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(tokyu6000_old_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_purchase_2c(0, 75) }
  GRAPHIC_STACK_3(tokyu6000_old_purchase, logo_tokyu, tokyu6000_old_purchase_c1, tokyu6000_old_purchase_c2)
//livery 1
spriteset(tokyu6000_old_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std(0, 100) }
spriteset(tokyu6000_old_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std_rev(0, 125) }
spriteset(tokyu6000_old_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std(0, 50) }
spriteset(tokyu6000_old_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std_rev(0, 50) }
spriteset(tokyu6000_old_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_symm(0, 75) }
spriteset(tokyu6000_old_front_1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std(0, 0) }
spriteset(tokyu6000_old_end_1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std_rev(0, 25) }
spriteset(tokyu6000_old_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std(0, 125) }
spriteset(tokyu6000_old_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/tokyu/tokyu6000_old/old6000.png"){ tmpl_std_rev(0, 125) }

//*livery 1
//special gfx
//head reversing checker
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_old_front_gfx,vehicle_is_reversed){
    1: tokyu6000_old_front;
    tokyu6000_old_front_1;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_old_end_gfx,vehicle_is_reversed){
    1: tokyu6000_old_end_1;
    tokyu6000_old_end;
}

//panto placer
//var 1
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_panto_placer,position_in_vehid_chain %2){
    1: tokyu6000_old_mot;
    tokyu6000_old_wag;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_panto_placer_rev,1-position_in_vehid_chain_from_end %2){
    1: tokyu6000_old_mot_rev;
    tokyu6000_old_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_old_panto_placer_main_1,vehicle_is_reversed){
    1:sw_tokyu6000_old_panto_placer_rev;
    sw_tokyu6000_old_panto_placer;
}

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_fake_placer,position_in_vehid_chain +1){
    5: tokyu6000_old_end_f;
    6: tokyu6000_old_front_f;
sw_tokyu6000_old_panto_placer_main_1;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_fake_placer_rev,position_in_vehid_chain_from_end ){
    5: tokyu6000_old_front_f;
    6: tokyu6000_old_end_f;
sw_tokyu6000_old_panto_placer_main_1;
}
switch(FEAT_TRAINS, PARENT, sw_tokyu6000_old_fake_placer_gfx,vehicle_is_reversed){
    1: sw_tokyu6000_old_fake_placer_rev;
    sw_tokyu6000_old_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_fake_placer_main,num_vehs_in_vehid_chain>8){
    1: sw_tokyu6000_old_fake_placer_gfx;
sw_tokyu6000_old_panto_placer_main_1;
}

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_end_gfx_main,position_in_vehid_chain_from_end){
    0: sw_tokyu6000_old_end_gfx;
    default: sw_tokyu6000_old_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_front_gfx_main,position_in_vehid_chain_from_end){
    0: sw_tokyu6000_old_front_gfx;
    default: sw_tokyu6000_old_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_livery_0,vehicle_type_id){
    mu_car_narrow:sw_tokyu6000_old_end_gfx_main;
    mu_car:sw_tokyu6000_old_end_gfx_main;
    sw_tokyu6000_old_front_gfx_main;
}

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_capacity,position_in_vehid_chain_from_end){
    0: 135*param_capacity_mod/3;
    default: 150*param_capacity_mod/3;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_capacity_1,position_in_vehid_chain){
    0: 135*param_capacity_mod/3;
    default: sw_tokyu6000_old_capacity;
}
switch(FEAT_TRAINS, SELF, sw_tokyu6000_old_capacity_main,vehicle_type_id){
    mu_car_narrow:sw_tokyu6000_old_capacity;
    mu_car:sw_tokyu6000_old_capacity;
    sw_tokyu6000_old_capacity_1;
}

//TODO: attachable vehicles -- will be changed later
switch(FEAT_TRAINS, SELF, tokyu6000_old_attach_vehid, vehicle_type_id) {
  mu_car: return string(STR_ATTACH_1067_ERROR);
  mu_car_metro: return string(STR_ATTACH_1067_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tokyu6000_old,2007) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_tokyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_TRAIN_NAME, string(STR_NAME_TOKYU), "6000", string(STR_NAME_SERIES));
        introduction_date: date(1960, 1, 1);
        model_life: 29;
        vehicle_life: 29;
        reliability_decay: 10;

        cost_factor: 21;
        running_cost_factor: 51;
        speed: 100 km/h;
        track_type: TR_1067_DC;
        power: 400 kW;
        weight: 28 ton;
        loading_speed: param_loading_3D;
    }
	graphics {
        default: sw_tokyu6000_old_livery_0;
        purchase: tokyu6000_old_purchase;
        can_attach_wagon: tokyu6000_old_attach_vehid;
        start_stop: sw_stop_start_2; 
        cargo_capacity: sw_tokyu6000_old_capacity_main; 
        //cargo_subtype_text: sw_{...}_lv_desc;
        additional_text:
		string(STR_TRAIN_DESC,
		string(STR_NARROWGAUGE),
		string(STR_DENKI, string(STR_DC_N, 1500)),
		string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
		string(STR_FORMATION, string(STR_NUM_CARS_4, 2,4,6,8)),
		string(STR_USAGE, string(STR_COMMUTER)),
		string(STR_OPERATOR, string(STR_TOKYU)));
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_tokyu6000_old_capacity_main;
        default: sw_tokyu6000_old_livery_0;
        power: 536;
        weight: 28;
      }
}