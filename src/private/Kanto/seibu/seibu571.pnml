spriteset(seibu571_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571_purchase.png"){ tmpl_purchase(0, 0) }
//livery 2

  spriteset(seibu571_front_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std(0, 0) }
  spriteset(seibu571_end_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std_rev(0, 25) }
  spriteset(seibu571_end_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std(0, 25) }
  spriteset(seibu571_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std(0, 50) }
  spriteset(seibu571_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std_rev(0, 75) }
  spriteset(seibu571_end_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std(0, 75) }
  spriteset(seibu571_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std(0, 100) }
  spriteset(seibu571_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_std_rev(0, 100) }
  spriteset(seibu571_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/seibu/seibu571/seibu571.png"){ tmpl_symm(0, 125) }
//*livery 1
  //special gfx
    switch(FEAT_TRAINS, PARENT, sw_seibu571_front_gfx_1, vehicle_is_reversed){
        1: seibu571_front;
        seibu571_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_seibu571_front_gfx, num_vehs_in_consist){
        2: sw_seibu571_front_gfx_1;
        seibu571_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_seibu571_end_gfx_1, vehicle_is_reversed){
        1: seibu571_end_mot;
        seibu571_end;
    }
    switch(FEAT_TRAINS, PARENT, sw_seibu571_end_gfx, num_vehs_in_consist){
        2: sw_seibu571_end_gfx_1;
        seibu571_end_mot;
    }

  //panto placer
      switch(FEAT_TRAINS, SELF, sw_seibu571_panto_placer,position_in_vehid_chain %4){
          2: seibu571_mot;
          seibu571_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_seibu571_panto_placer_rev,position_in_vehid_chain_from_end %4 -1){
          2: seibu571_mot_rev;
          seibu571_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_seibu571_panto_placer_main,vehicle_is_reversed){
          1: sw_seibu571_panto_placer_rev;
          sw_seibu571_panto_placer;
      }
  //fake head-end placer
    switch(FEAT_TRAINS, SELF, sw_seibu571_fake_placer,position_in_vehid_chain +1){
			3: seibu571_end_mot;
			4: seibu571_end_mot_rev;
    sw_seibu571_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_seibu571_fake_placer_rev,position_in_vehid_chain_from_end ){
			3: seibu571_end_mot_rev;
			4: seibu571_end_mot;
    sw_seibu571_panto_placer_main;
    }
    switch(FEAT_TRAINS, PARENT, sw_seibu571_fake_placer_gfx,vehicle_is_reversed){
			1: sw_seibu571_fake_placer_rev;
			sw_seibu571_fake_placer;
    }
    switch(FEAT_TRAINS, SELF, sw_seibu571_fake_placer_main,num_vehs_in_vehid_chain>5 - 1){
			1: sw_seibu571_fake_placer_gfx;
    	sw_seibu571_panto_placer_main;
    }
  //front end fixer
    switch(FEAT_TRAINS, SELF, sw_seibu571_end_fix, position_in_consist_from_end){
        0:sw_seibu571_end_gfx;
        sw_seibu571_fake_placer_main;
    }

    switch(FEAT_TRAINS, SELF, sw_seibu571_lv,vehicle_type_id){
        mu_car_narrow:sw_seibu571_end_fix;
        sw_seibu571_front_gfx;
    }
//
//*post fixers
  //capacity fix
    switch(FEAT_TRAINS, SELF, sw_seibu571_capacity,position_in_vehid_chain_from_end){
        0: return 160;
        return 168;
    }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_seibu571_power,position_in_vehid_chain_from_end %2){
      0: return 0;
      return 400*1342/1000;
    }
  //attachable vehicles
  switch(FEAT_TRAINS, SELF, seibu571_attach_vehid, vehicle_type_id) {
			mu_car: return string(STR_ATTACH_1067_ERROR);
			mu_car_metro: return string(STR_ATTACH_1067_ERROR);
			return CB_RESULT_ATTACH_ALLOW;
		}

item(FEAT_TRAINS, seibu571,2112) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_seibu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_TRAIN_NAME, string(STR_NAME_SEIBU), "571", string(STR_NAME_SERIES));
        introduction_date: date(1978, 1, 1);
        model_life: 36;
        vehicle_life: 31;
        reliability_decay: 10;

        cost_factor: 19;
        running_cost_factor: 48;
        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 400 kW;
        weight: 40 ton;
        loading_speed: param_loading_2D;
        variant_group:2111;
    }
	graphics {
        start_stop: sw_stop_start_2;
        loading_speed: param_loading_3D;
        cargo_capacity: sw_seibu571_capacity()*param_capacity_mod/3;  
        default: sw_seibu571_lv;
        purchase: seibu571_purchase;
        can_attach_wagon: seibu571_attach_vehid;
        //cargo_subtype_text:sw_seibu571_lv_desc;
        additional_text:    string(STR_TRAIN_DESC,
                            string(STR_NARROWGAUGE),
                            string(STR_LOAD_SPEED,
                            string(STR_DOORS, 3),),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_2, 2,4),
                            // string(STR_COUPLER,
                            // string(STR_SHIBATA)),
                            string(STR_USAGE,
                            string(STR_COMMUTER),
                            string(STR_DC),
                            ),
                            string(STR_OPERATOR,
                            string(STR_SEIBU)));       
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_seibu571_capacity()*param_capacity_mod/3;
        default:                      sw_seibu571_lv;
        power: sw_seibu571_power;
        weight: 30;
      }
}