spriteset (ng_sk_7000series_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(ng_sk_7000series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (ng_sk_7000series_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (ng_sk_7000series_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }
spriteset (ng_sk_7000series_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (ng_sk_7000series_mot_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (ng_sk_7000series_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (ng_sk_7000series_front_f,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (ng_sk_7000series_end_f,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }

alternative_sprites(ng_sk_7000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std(0, 0) }
alternative_sprites(ng_sk_7000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(ng_sk_7000series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std(0, 50) }
alternative_sprites(ng_sk_7000series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(ng_sk_7000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std(0, 75) }
alternative_sprites(ng_sk_7000series_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std(0, 100) }
alternative_sprites(ng_sk_7000series_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/kamiida/7000series/7000series.png"){ tmpl_std_rev(0, 100) }

//livery 1
//special gfx
//panto placer
//1
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_panto_placer,position_in_consist %3){
    1: ng_sk_7000series_mot_rev;
    2: ng_sk_7000series_mot;
    ng_sk_7000series_wag;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_panto_placer_rev,position_in_consist_from_end %3){
    1: ng_sk_7000series_mot;
    2: ng_sk_7000series_mot_rev;
    ng_sk_7000series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_ng_sk_7000series_panto_placer_main,vehicle_is_reversed){
    1: sw_ng_sk_7000series_panto_placer_rev;
    sw_ng_sk_7000series_panto_placer;
}

//panto placer end 

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_fake_placer,position_in_consist){
    3: ng_sk_7000series_end_f;
    4: ng_sk_7000series_front_f;
sw_ng_sk_7000series_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_fake_placer_rev,position_in_consist_from_end){
    3: ng_sk_7000series_front_f;
    4: ng_sk_7000series_end_f;
sw_ng_sk_7000series_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_ng_sk_7000series_fake_placer_gfx,vehicle_is_reversed){
    1: sw_ng_sk_7000series_fake_placer_rev;
    sw_ng_sk_7000series_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_fake_placer_main,num_vehs_in_consist>6){
    1: sw_ng_sk_7000series_fake_placer_gfx;
sw_ng_sk_7000series_panto_placer_main;
}
//fake head-end placer end

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_end_gfx,position_in_consist_from_end){
    0: ng_sk_7000series_end;
    default: sw_ng_sk_7000series_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_livery_1,position_in_consist){
    0: ng_sk_7000series_front;
    default: sw_ng_sk_7000series_end_gfx;
}

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_ng_sk_7000series_main_capacity,position_in_consist_from_end){
    0: 128*param_capacity_mod/3;
    default: 135*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, ng_sk_7000series_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, ng_sk_7000series,1410) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_nagoya == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_ng_sk_7000series_NAME);
        introduction_date: date(2003, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 22;

        cost_factor: 20;
        running_cost_factor: 50;
        speed: 120 km/h;
        track_type: TR_1067;
        power: 680 kW;
        weight: 32 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_ng_sk_7000series_livery_1;
        purchase: ng_sk_7000series_purchase;
        can_attach_wagon: ng_sk_7000series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_4_CARS),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_NAGOYA,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_NAGOYA_KAMIIDA)
                            ),
                        );
        start_stop: sw_stop_start_3; 
        cargo_capacity: 128*param_capacity_mod/3;  
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_ng_sk_7000series_main_capacity;
        default:                      sw_ng_sk_7000series_livery_1;
        power: 912/3;
        weight: 33;
        length: 8; 
      }
}