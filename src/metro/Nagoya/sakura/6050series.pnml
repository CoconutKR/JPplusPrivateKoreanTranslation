spriteset (ng_sk_6050series_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(ng_sk_6050series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (ng_sk_6050series_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (ng_sk_6050series_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }
spriteset (ng_sk_6050series_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (ng_sk_6050series_mot_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (ng_sk_6050series_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (ng_sk_6050series_front_f,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (ng_sk_6050series_end_f,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 25) }

alternative_sprites(ng_sk_6050series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std(0, 0) }
alternative_sprites(ng_sk_6050series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(ng_sk_6050series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std(0, 50) }
alternative_sprites(ng_sk_6050series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(ng_sk_6050series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std(0, 75) }
alternative_sprites(ng_sk_6050series_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std(0, 100) }
alternative_sprites(ng_sk_6050series_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Nagoya/sakura/6050series/6050series.png"){ tmpl_std_rev(0, 100) }

//*livery 1
//special gfx
//panto placer
//1
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_panto_placer,position_in_consist %6){
    1: ng_sk_6050series_mot;
    2: ng_sk_6050series_mot;
    3: ng_sk_6050series_mot;
    ng_sk_6050series_wag;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_panto_placer_rev,position_in_consist_from_end %6){
    1: ng_sk_6050series_mot_rev;
    2: ng_sk_6050series_mot_rev;
    3: ng_sk_6050series_mot_rev;
    ng_sk_6050series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_ng_sk_6050series_panto_placer_main_1,vehicle_is_reversed){
    1: sw_ng_sk_6050series_panto_placer_rev;
    sw_ng_sk_6050series_panto_placer;
}
//2
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_1_panto_placer,position_in_consist %8){
    1: ng_sk_6050series_mot;
    2: ng_sk_6050series_mot;
    4: ng_sk_6050series_mot;
    6: ng_sk_6050series_mot;
    ng_sk_6050series_wag;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_1_panto_placer_rev,position_in_consist_from_end %8){
    1: ng_sk_6050series_mot_rev;
    2: ng_sk_6050series_mot_rev;
    4: ng_sk_6050series_mot_rev;
    6: ng_sk_6050series_mot_rev;
    ng_sk_6050series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_ng_sk_6050series_panto_placer_main_2,vehicle_is_reversed){
    1: sw_ng_sk_6050series_1_panto_placer_rev;
    sw_ng_sk_6050series_1_panto_placer;
}

switch(FEAT_TRAINS, PARENT, sw_ng_sk_6050series_panto_placer_main,num_vehs_in_consist){
0..5:sw_ng_sk_6050series_panto_placer_main_1;
sw_ng_sk_6050series_panto_placer_main_2;
}
//panto placer end 

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_fake_placer,position_in_consist){
    4: ng_sk_6050series_end_f;
    5: ng_sk_6050series_front_f;
sw_ng_sk_6050series_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_fake_placer_rev,position_in_consist_from_end){
    4: ng_sk_6050series_front_f;
    5: ng_sk_6050series_end_f;
sw_ng_sk_6050series_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_ng_sk_6050series_fake_placer_gfx,vehicle_is_reversed){
    1: sw_ng_sk_6050series_fake_placer_rev;
    sw_ng_sk_6050series_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_fake_placer_main,num_vehs_in_consist>7){
    1: sw_ng_sk_6050series_fake_placer_gfx;
sw_ng_sk_6050series_panto_placer_main;
}
//fake head-end placer end

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_end_gfx,position_in_consist_from_end){
    0: ng_sk_6050series_end;
    default: sw_ng_sk_6050series_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_livery_1,position_in_consist){
    0: ng_sk_6050series_front;
    default: sw_ng_sk_6050series_end_gfx;
}

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_ng_sk_6050series_main_capacity,position_in_consist_from_end){
    0: 136*param_capacity_mod/3;
    default: 147*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, ng_sk_6050series_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, ng_sk_6050series,1401) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_nagoya == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_ng_sk_6050series_NAME);
        introduction_date: date(2009, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 20;

        cost_factor: 20;
        running_cost_factor: 50;
        speed: 120 km/h;
        track_type: TR_1067_DC;
        power: 680 kW;
        weight: 34 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_ng_sk_6050series_livery_1;
        purchase: ng_sk_6050series_purchase;
        can_attach_wagon: ng_sk_6050series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            
                            string(STR_ng_sk_6050series_DESC),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_NAGOYA,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_NAGOYA_SAKURA)
                            ),
                        );
        start_stop: sw_stop_start_3; 
        cargo_capacity: 136*param_capacity_mod/3;  
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_ng_sk_6050series_main_capacity;
        default:                      sw_ng_sk_6050series_livery_1;
        power: 912*2/5;
        weight: 34;
        length: 8; 
      }
}