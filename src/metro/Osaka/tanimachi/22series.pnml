//purchase
spriteset(os_tm_22series_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series.png"){ tmpl_purchase_1c(0, 0) }
spriteset(os_tm_22series_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_4(os_tm_22series_purchase, logo_osaka_metro, logo_line_os_tani, os_tm_22series_purchase_c1, os_tm_22series_purchase_c2)

spriteset(os_tm_22series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series.png"){ tmpl_std(0, 0) }
spriteset(os_tm_22series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series.png"){ tmpl_std_rev(0, 25) }
spriteset(os_tm_22series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series.png"){ tmpl_std(0, 50) }

spriteset(os_tm_22series_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series_1.png"){ tmpl_std(0, 0) }
spriteset(os_tm_22series_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series_1.png"){ tmpl_std_rev(0, 25) }
spriteset(os_tm_22series_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/tanimachi/22series/22series_1.png"){ tmpl_std(0, 50) }

//*livery 1
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_tm_22series_end_gfx,position_in_consist_from_end){
    0: os_tm_22series_end;
    default: os_tm_22series_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_tm_22series_livery_1,position_in_consist){
    0: os_tm_22series_front;
    default: sw_os_tm_22series_end_gfx;
}
//*livery 2
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_tm_22series_1_end_gfx,position_in_consist_from_end){
    0: os_tm_22series_1_end;
    default: os_tm_22series_1_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_tm_22series_livery_2,position_in_consist){
    0: os_tm_22series_1_front;
    default: sw_os_tm_22series_1_end_gfx;
}
//randomized livery
random_switch(FEAT_TRAINS, PARENT, sw_os_tm_22series_livery_0){
    1: sw_os_tm_22series_livery_1;
    1: sw_os_tm_22series_livery_2;
}

//timed-livery selector
switch(FEAT_TRAINS, SELF, sw_os_tm_22series_lv_desc, cargo_subtype){
    0: string(LV_RANDOM);
    1: string(LV_os_tm_22series_1);
    2: string(LV_os_tm_22series_2);
}
//liveries
switch(FEAT_TRAINS, PARENT, sw_os_tm_22series_lv, cargo_subtype){
    0: sw_os_tm_22series_livery_0;
    1: sw_os_tm_22series_livery_1;
    2: sw_os_tm_22series_livery_2;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_os_tm_22series_main_capacity,position_in_consist_from_end){
    0: 130*param_capacity_mod/3;
    default: 140*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, os_tm_22series_attach_vehid, vehicle_type_id) {
  mu_car_metro: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, os_tm_22series,1253) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_OSAKA_METRO), "22", string(STR_NAME_SERIES));
        introduction_date: date(1990, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 130;
        loading_speed: param_loading_4D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 90 km/h;
        track_type: TR_1435_M;
        power: 560 kW;
        weight: 36 ton;

        climates_available: param_disable_osaka == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                
        variant_group: 1237;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_os_tm_22series_lv;
        purchase: os_tm_22series_purchase;
        can_attach_wagon: os_tm_22series_attach_vehid;
        additional_text:
    string(STR_TRAIN_DESC,
		string(STR_STANDARDGAUGE),
		string(STR_DENKI, string(STR_DC_N, 750)),
		string(STR_LOAD_SPEED, string(STR_DOORS, 4)),
		string(STR_FORMATION, string(STR_NUM_CARS_1, 6)),
		string(STR_OPERATOR, string(STR_OSAKAMETRO)),
string(STR_LINES_USED, string(STR_OSAKA_TANIMACHI)));

        cargo_subtype_text: sw_os_tm_22series_lv_desc;
        start_stop: sw_stop_start_2; 
        cargo_capacity: 130*param_capacity_mod/3;  
    }
	livery_override((mu_car_metro)){
        cargo_subtype_text: sw_os_tm_22series_lv_desc;
        loading_speed: param_loading_4D;
        cargo_capacity: sw_os_tm_22series_main_capacity;
        default: sw_os_tm_22series_lv;
        power: 753*2/5;
        weight: 32;
        length: 8;
      }
}