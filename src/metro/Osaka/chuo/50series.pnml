//purchase
spriteset(os_ch_50series_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series.png"){ tmpl_purchase_1c(0, 0) }
spriteset(os_ch_50series_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_4(os_ch_50series_purchase, logo_osaka_metro, logo_line_os_chuo, os_ch_50series_purchase_c1, os_ch_50series_purchase_c2)
//
spriteset(os_ch_50series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series.png"){ tmpl_std(0, 0) }
spriteset(os_ch_50series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series.png"){ tmpl_std_rev(0, 25) }
spriteset(os_ch_50series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series.png"){ tmpl_std(0, 50) }
spriteset(os_ch_50series_wag_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series.png"){ tmpl_std_rev(0, 50) }
//
spriteset(os_ch_50series_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series_1.png"){ tmpl_std(0, 0) }
spriteset(os_ch_50series_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series_1.png"){ tmpl_std_rev(0, 25) }
spriteset(os_ch_50series_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series_1.png"){ tmpl_std(0, 50) }
spriteset(os_ch_50series_1_wag_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/chuo/50series/50series_1.png"){ tmpl_std_rev(0, 50) }


//*livery white
//wagon specialgfx
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_wag_1_gfx,position_in_vehid_chain %2){
    0: os_ch_50series_wag_rev;
    os_ch_50series_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_wag_rev_gfx,position_in_vehid_chain_from_end %2 -1){
    0: os_ch_50series_wag;
    os_ch_50series_wag_rev;
}
switch(FEAT_TRAINS, PARENT, sw_os_ch_50series_wag_gfx,vehicle_is_reversed){
    1: sw_os_ch_50series_wag_rev_gfx;
    sw_os_ch_50series_wag_1_gfx;
}
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_end_gfx,position_in_vehid_chain_from_end){
    0: os_ch_50series_end;
    sw_os_ch_50series_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_livery_1,vehicle_type_id){
    os_ch_50series: os_ch_50series_front;
    sw_os_ch_50series_end_gfx;
}

//*livery green
//wagon specialgfx
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_1_wag_1_gfx,position_in_vehid_chain %2){
    0: os_ch_50series_1_wag_rev;
    os_ch_50series_1_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_1_wag_rev_gfx,position_in_vehid_chain_from_end %2 -1){
    0: os_ch_50series_1_wag;
    os_ch_50series_1_wag_rev;
}
switch(FEAT_TRAINS, PARENT, sw_os_ch_50series_1_wag_gfx,vehicle_is_reversed){
    1: sw_os_ch_50series_1_wag_1_gfx;
    sw_os_ch_50series_1_wag_rev_gfx;
}
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_1_end_gfx,position_in_vehid_chain_from_end){
    0: os_ch_50series_1_end;
    sw_os_ch_50series_1_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_livery_2,vehicle_type_id){
    os_ch_50series: os_ch_50series_1_front;
    sw_os_ch_50series_1_end_gfx;
}

//randomizer
random_switch(FEAT_TRAINS, PARENT, sw_os_ch_50series_livery_0){
    1: sw_os_ch_50series_livery_1;
    1: sw_os_ch_50series_livery_2;
}

//livery selector
switch(FEAT_TRAINS, SELF, sw_os_ch_50series_lv_desc, cargo_subtype){
    0: string(LV_RANDOM);
    1: string(LV_DATED, string(STR_PAINT,string(STR_WHITE)),1967);
    2: string(LV_DATED, string(STR_PAINT,string(STR_GREEN)),1967);
}
//liveries
switch(FEAT_TRAINS, PARENT, sw_os_ch_50series_lv, cargo_subtype){
    0: sw_os_ch_50series_livery_0;
    1: sw_os_ch_50series_livery_1;
    2: sw_os_ch_50series_livery_2;
}

item(FEAT_TRAINS, os_ch_50series,1271) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_OSAKA_METRO), "50", string(STR_NAME_SERIES));
        introduction_date: date(1967, 1, 1);
        model_life: 25;
        vehicle_life: 32;
        reliability_decay: 10;

        cargo_capacity: 72;
        loading_speed: param_loading_3D;
        cost_factor: 16;
        running_cost_factor: 46;

        speed: 70 km/h;
        track_type: TR_1435_M;
        power: 360 kW;
        weight: 36 ton;

        climates_available: param_disable_osaka == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                
        variant_group: 1234;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_os_ch_50series_lv;
		purchase: os_ch_50series_purchase;
		can_attach_wagon: sw_1435_m_attach_vehid;
		additional_text:
 		   	string(STR_TRAIN_DESC,
				string(STR_STANDARDGAUGE),
				string(STR_DENKI, string(STR_DC_N, 750)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
				string(STR_FORMATION, string(STR_NUM_CARS_1, 4)),
				string(STR_OPERATOR, string(STR_OSAKAMETRO)),
string(STR_LINES_USED, string(STR_OSAKA_CHUO)));

		cargo_subtype_text: sw_os_ch_50series_lv_desc;
		start_stop: sw_stop_start_2; 
        cargo_capacity: 72*param_capacity_mod/3;  
    }
	livery_override(mu_car_metro){
		cargo_subtype_text: sw_os_ch_50series_lv_desc;
        loading_speed: param_loading_3D;
        cargo_capacity: 72*param_capacity_mod/3;
        default: sw_os_ch_50series_lv;
        power: 483;
        weight: 36;
        length: 8;
      }
}