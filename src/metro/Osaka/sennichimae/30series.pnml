//purchase
spriteset(os_sn_30series_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series.png"){ tmpl_purchase_1c(0, 0) }
spriteset(os_sn_30series_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_4(os_sn_30series_purchase, logo_osaka_metro, logo_line_os_sennichi, os_sn_30series_purchase_c1, os_sn_30series_purchase_c2)
//steel old
//
spriteset(os_sn_30series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series.png"){ tmpl_std(0, 0) }
spriteset(os_sn_30series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series.png"){ tmpl_std_rev(0, 25) }
spriteset(os_sn_30series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series.png"){ tmpl_std(0, 50) }
//alumi old
//
spriteset(os_sn_30series_al_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series_al.png"){ tmpl_std(0, 0) }
spriteset(os_sn_30series_al_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series_al.png"){ tmpl_std_rev(0, 25) }
spriteset(os_sn_30series_al_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/30series/30series_al.png"){ tmpl_std(0, 50) }

//steel old
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_sn_30series_end_gfx,position_in_vehid_chain_from_end){
    0: os_sn_30series_end;
    os_sn_30series_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_sn_30series_livery_1,vehicle_type_id){
    os_sn_30series: os_sn_30series_front;
    sw_os_sn_30series_end_gfx;
}
//alumi old
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_sn_30series_al_end_gfx,position_in_vehid_chain_from_end){
    0: os_sn_30series_al_end;
    os_sn_30series_al_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_sn_30series_livery_2,vehicle_type_id){
    os_sn_30series: os_sn_30series_al_front;
    sw_os_sn_30series_al_end_gfx;
}

//randomizer
random_switch(FEAT_TRAINS, PARENT, sw_os_sn_30series_livery_0){
    1: sw_os_sn_30series_livery_1;
    1: sw_os_sn_30series_livery_2;
}
//livery selector desc
switch(FEAT_TRAINS, SELF, sw_os_sn_30series_lv_desc, cargo_subtype){
    0: string(LV_RANDOM);
    1: string(LV_DATED, string(STR_LIVERY, string(STR_STEEL)), 1967);
    2: string(LV_DATED, string(STR_LIVERY, string(STR_ALUMI)), 1968);
}
//livery selector 
switch(FEAT_TRAINS, PARENT, sw_os_sn_30series_lv, cargo_subtype){
    0: sw_os_sn_30series_livery_0;
    1: sw_os_sn_30series_livery_1;
    2: sw_os_sn_30series_livery_2;
}

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_os_sn_30series_main_capacity,position_in_consist_from_end){
    0: 91*param_capacity_mod/3;
    default: 95*param_capacity_mod/3;
}

item(FEAT_TRAINS, os_sn_30series,1282) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_OSAKA_METRO), "30", string(STR_NAME_SERIES));
        introduction_date: date(1967, 1, 1);
        model_life: 44;
        vehicle_life: 44;
        reliability_decay: 10;

        cargo_capacity: 91;
        loading_speed: param_loading_4D;
        cost_factor: 17;
        running_cost_factor: 47;

        speed: 80 km/h;
        track_type: TR_1435_M;
        power: 480 kW;
        weight: 30 ton;

        climates_available: param_disable_osaka == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        variant_group: 1235;

        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_os_sn_30series_lv;
		purchase: os_sn_30series_purchase;
		can_attach_wagon: sw_1435_m_attach_vehid;
		additional_text:
 		   	string(STR_TRAIN_DESC,
				string(STR_STANDARDGAUGE),
				string(STR_DENKI, string(STR_DC_N, 750)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 4)),
				string(STR_FORMATION, string(STR_NUM_CARS_1, 4)),
				string(STR_OPERATOR, string(STR_OSAKAMETRO)),
string(STR_LINES_USED, string(STR_OSAKA_SENNICHI)));

		cargo_subtype_text: sw_os_sn_30series_lv_desc;
		start_stop: sw_stop_start_3; 
        cargo_capacity: 91*param_capacity_mod/3;
        
    }
	livery_override(mu_car_metro){
		cargo_subtype_text: sw_os_sn_30series_lv_desc;
        loading_speed: param_loading_4D;
        cargo_capacity: sw_os_sn_30series_main_capacity;
        default: sw_os_sn_30series_lv;
        power: 644*3/5;
        weight: 32;
        length: 8;
      }
}