spriteset (os_sn_25series_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(os_sn_25series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (os_sn_25series_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (os_sn_25series_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 25) }
spriteset (os_sn_25series_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }

spriteset (os_sn_25series_1_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (os_sn_25series_1_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 25) }
spriteset (os_sn_25series_1_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }

alternative_sprites(os_sn_25series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series.png"){ tmpl_std(0, 0) }
alternative_sprites(os_sn_25series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(os_sn_25series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series.png"){ tmpl_std(0, 50) }

alternative_sprites(os_sn_25series_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series_1.png"){ tmpl_std(0, 0) }
alternative_sprites(os_sn_25series_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series_1.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(os_sn_25series_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Osaka/sennichimae/25series/25series_1.png"){ tmpl_std(0, 50) }

//*livery 1
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_sn_25series_end_gfx,position_in_consist_from_end){
    0: os_sn_25series_end;
    default: os_sn_25series_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_sn_25series_livery_1,position_in_consist){
    0: os_sn_25series_front;
    default: sw_os_sn_25series_end_gfx;
}
//*livery 2
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_os_sn_25series_1_end_gfx,position_in_consist_from_end){
    0: os_sn_25series_1_end;
    default: os_sn_25series_1_wag;
}
switch(FEAT_TRAINS, SELF, sw_os_sn_25series_livery_2,position_in_consist){
    0: os_sn_25series_1_front;
    default: sw_os_sn_25series_1_end_gfx;
}
//randomized livery
random_switch(FEAT_TRAINS, PARENT, sw_os_sn_25series_livery_0){
    1: sw_os_sn_25series_livery_1;
    1: sw_os_sn_25series_livery_2;
}

//timed-livery selector
switch(FEAT_TRAINS, SELF, sw_os_sn_25series_lv_desc, cargo_subtype){
    0: string(LV_TIME_RANDOM);
    1: string(LV_os_sn_25series_1);
    2: string(LV_os_sn_25series_2);
}
//liveries
switch(FEAT_TRAINS, PARENT, sw_os_sn_25series_lv, cargo_subtype){
    0: sw_os_sn_25series_livery_0;
    1: sw_os_sn_25series_livery_1;
    2: sw_os_sn_25series_livery_2;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_os_sn_25series_main_capacity,position_in_consist_from_end){
    0: 130*param_capacity_mod/3;
    default: 140*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, os_sn_25series_attach_vehid, vehicle_type_id) {
  mu_car_metro: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, os_sn_25series,1283) {
    property {
        name: string(STR_os_sn_25series_NAME);
        introduction_date: date(1991, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 23;

        cargo_capacity: 130;
        loading_speed: param_loading_4D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 95 km/h;
        track_type: TR_1435_M;
        power: 560 kW;
        weight: 36 ton;

        climates_available: param_disable_osaka == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        variant_group: 1237;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_os_sn_25series_lv;
        purchase: os_sn_25series_purchase;
        can_attach_wagon: os_sn_25series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_4_CARS),
                            string(STR_CARS),
                            string(STR_OPERATOR_PRIVATE, 
                            string(STR_OSAKAMETRO)),
string(STR_LINES_USED,
                            string(STR_OSAKA_SENNICHI)
                            ),
                        );
        cargo_subtype_text: sw_os_sn_25series_lv_desc;
        start_stop: sw_stop_start_4; 
        cargo_capacity: 130*param_capacity_mod/3;  
    }
    livery_override(mu_car_metro){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_os_sn_25series_main_capacity;
        default:                      sw_os_sn_25series_lv;
        power: 753/3;
        weight: 32;
        length: 8;
      }
}