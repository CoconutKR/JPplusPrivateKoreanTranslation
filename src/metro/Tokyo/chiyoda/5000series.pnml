spriteset (tk_ch_5000series_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_ch_5000series_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_ch_5000series_front_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_ch_5000series_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_ch_5000series_wag_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_ch_5000series_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_ch_5000series_mot_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_ch_5000series_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
spriteset (tk_ch_5000series_end_mot,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
spriteset (tk_ch_5000series_mot_1,       "gfx/gfx_placeholder.png") { tmpl_std(0, 50) }
spriteset (tk_ch_5000series_mot_1_rev,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }

alternative_sprites(tk_ch_5000series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_ch_5000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_ch_5000series_front_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std(0, 100) }
alternative_sprites(tk_ch_5000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std(0, 50) }
alternative_sprites(tk_ch_5000series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std(0, 75) }
alternative_sprites(tk_ch_5000series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std_rev(0, 75) }
alternative_sprites(tk_ch_5000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tk_ch_5000series_end_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std_rev(0, 125) }
alternative_sprites(tk_ch_5000series_mot_1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std(0, 150) }
alternative_sprites(tk_ch_5000series_mot_1_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/chiyoda/5000series/5000series.png"){ tmpl_std_rev(0, 150) }

//livery 1
//panto-placers
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_mot_def_gfx,position_in_consist %10){
    1: tk_ch_5000series_mot_1;
    3: tk_ch_5000series_mot_1;
    6: tk_ch_5000series_mot_1_rev;
    8: tk_ch_5000series_mot_1_rev;
    default: tk_ch_5000series_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_mot_def_rev_gfx,position_in_consist_from_end %10){
    1: tk_ch_5000series_mot_1_rev;
    3: tk_ch_5000series_mot_1_rev;
    6: tk_ch_5000series_mot_1;
    8: tk_ch_5000series_mot_1;
    default: tk_ch_5000series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tk_ch_5000series_mot_def_main_gfx,vehicle_is_reversed){
    1: sw_tk_ch_5000series_mot_def_rev_gfx;
    sw_tk_ch_5000series_mot_def_gfx;
}
//special gfx



switch(FEAT_TRAINS, SELF, tk_ch_5000series_main,position_in_consist %3){
    0: tk_ch_5000series_front_mot;
    2: tk_ch_5000series_end;
    tk_ch_5000series_mot_1;
}
switch(FEAT_TRAINS, SELF, tk_ch_5000series_main_rev,position_in_consist %3){
    0: tk_ch_5000series_front;
    2: tk_ch_5000series_end_mot;
    tk_ch_5000series_mot_1_rev;
}
switch(FEAT_TRAINS, PARENT, tk_ch_5000_br_rev_check, vehicle_is_reversed){
0: tk_ch_5000series_main;
tk_ch_5000series_main_rev;
}
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_end_gfx,position_in_consist_from_end){
    0: tk_ch_5000series_end;
    default: sw_tk_ch_5000series_mot_def_main_gfx;
}

switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_front_gfx,position_in_consist){
    0: tk_ch_5000series_front;
    default: sw_tk_ch_5000series_end_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_10_def_gfx,position_in_consist %10){
    4: tk_ch_5000series_end;
    5: tk_ch_5000series_front; 
    sw_tk_ch_5000series_front_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_checker,num_vehs_in_consist){
0..9: sw_tk_ch_5000series_front_gfx;
10: sw_tk_ch_5000series_10_def_gfx;
11..12: sw_tk_ch_5000series_front_gfx;
sw_tk_ch_5000series_10_def_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_livery_1,num_vehs_in_consist){
3: tk_ch_5000_br_rev_check;
default: sw_tk_ch_5000series_checker;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tk_ch_5000series_main_capacity,position_in_consist_from_end){
    0: 136*param_capacity_mod/3;
    default: 144*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_ch_5000series_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}


item(FEAT_TRAINS, tk_ch_5000series,1060) {
    property {
        name: string(STR_tk_ch_5000series_NAME);
        introduction_date: date(1969, 1, 1);
        model_life: 10;
        vehicle_life: 38;
        reliability_decay: 21;

        cargo_capacity: 136;
        loading_speed: param_loading_4D;
        cost_factor: 19;
        running_cost_factor: 49;

        speed: 100 km/h;
        track_type: TR_1067;
        power: 400 kW;
        weight: 27 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        default: sw_tk_ch_5000series_livery_1;
        purchase: tk_ch_5000series_purchase;
        can_attach_wagon: tk_ch_5000series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_tk_ch_5000series_DESC),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_TOKYOMETRO,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_CHIYODA)
                            ),
                        );
        start_stop: sw_stop_start_3;
        cargo_capacity: 136*param_capacity_mod/3;   
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tk_ch_5000series_main_capacity; 
        default:                      sw_tk_ch_5000series_livery_1;
        power: 536*5/9;
        weight: 27;
        length: 8;
      }
}