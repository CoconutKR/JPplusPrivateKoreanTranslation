spriteset (tk_hb_eidan3000_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_hb_eidan3000_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_hb_eidan3000_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan3000_mot_rev,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan3000_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan3000_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
alternative_sprites(tk_hb_eidan3000_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan3000/eidan3000_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_hb_eidan3000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan3000/eidan3000.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_hb_eidan3000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan3000/eidan3000.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tk_hb_eidan3000_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan3000/eidan3000.png"){ tmpl_std(0, 50) }
alternative_sprites(tk_hb_eidan3000_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan3000/eidan3000.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(tk_hb_eidan3000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan3000/eidan3000.png"){ tmpl_std(0, 75) }

//setup
//panto-placers
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_mot_def_gfx,position_in_consist %3){
2: tk_hb_eidan3000_mot;
default: tk_hb_eidan3000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_mot_def_rev_gfx,position_in_consist_from_end %3){
2: tk_hb_eidan3000_mot_rev;
default: tk_hb_eidan3000_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tk_hb_eidan3000_mot_def_main_gfx,vehicle_is_reversed){
    1: sw_tk_hb_eidan3000_mot_def_rev_gfx;
    sw_tk_hb_eidan3000_mot_def_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_mot_8_gfx,position_in_consist %4){
    0: tk_hb_eidan3000_front;
    1: tk_hb_eidan3000_wag;
    2: tk_hb_eidan3000_wag;
    3: tk_hb_eidan3000_end;
}

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_end_gfx,position_in_consist_from_end){
    0: tk_hb_eidan3000_end;
    1: tk_hb_eidan3000_wag;
    default: sw_tk_hb_eidan3000_mot_def_main_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_front_gfx,position_in_consist){
    0: tk_hb_eidan3000_front;
    1: tk_hb_eidan3000_wag;
    default: sw_tk_hb_eidan3000_end_gfx;
}
//main gfx 
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_main_gfx,num_vehs_in_consist){
8: sw_tk_hb_eidan3000_mot_8_gfx;
16: sw_tk_hb_eidan3000_mot_8_gfx;
32: sw_tk_hb_eidan3000_mot_8_gfx;
default: sw_tk_hb_eidan3000_front_gfx;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan3000_main_capacity,position_in_consist_from_end){
    0: 120*param_capacity_mod/3;
    default: 128*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_hb_eidan3000_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}


item(FEAT_TRAINS, tk_hb_eidan3000, 1030) {
    property {
        name: string(STR_tk_hn_eidan3000_NAME);
        introduction_date: date(1961, 1, 1);
        model_life: 32;
        vehicle_life: 33;

        speed: 100 km/h;
        track_type: TR_1067;
        power: 300 kW;
        weight: 33 ton;

        cargo_capacity: 120;
        loading_speed: param_loading_3D;

        reliability_decay: 22;
        cost_factor: 19;
        running_cost_factor: 49;
        
        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_tk_hb_eidan3000_main_gfx;
        purchase: tk_hb_eidan3000_purchase;
        can_attach_wagon: tk_hb_eidan3000_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_tk_hn_eidan3000_DESC),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_TOKYOMETRO,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_HIBIYA)
                            ),
                        );
        length: 8;
        start_stop: sw_stop_start_2;
        cargo_capacity: 120*param_capacity_mod/3;
    }
    livery_override(mu_car_narrow){
        default:                      sw_tk_hb_eidan3000_main_gfx;
        cargo_capacity:               sw_tk_hb_eidan3000_main_capacity;
        loading_speed:                param_loading_3D;
        //cargo_subtype_text:           sw_6340_subtypetext_mucar;
        power: 403;
        weight: 31;
        length: 8;
      }
}