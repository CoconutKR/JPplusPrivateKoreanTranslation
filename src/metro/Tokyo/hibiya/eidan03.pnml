spriteset (tk_hb_eidan03_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_hb_eidan03_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_hb_eidan03_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan03_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan03_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
alternative_sprites(tk_hb_eidan03_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_hb_eidan03_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_hb_eidan03_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tk_hb_eidan03_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03.png"){ tmpl_std(0, 50) }
alternative_sprites(tk_hb_eidan03_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03.png"){ tmpl_std(0, 75) }
spriteset (tk_hb_eidan03_1_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_hb_eidan03_1_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan03_1_mot,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hb_eidan03_1_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
alternative_sprites(tk_hb_eidan03_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03_5d.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_hb_eidan03_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03_5d.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tk_hb_eidan03_1_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03_5d.png"){ tmpl_std(0, 50) }
alternative_sprites(tk_hb_eidan03_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/eidan03/eidan03_5d.png"){ tmpl_std(0, 75) }


//*livery 1
//setup
//panto-placers
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_mot_def_gfx,position_in_consist %3){
1: tk_hb_eidan03_mot;
default: tk_hb_eidan03_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_mot_def_rev_gfx,position_in_consist_from_end %3){
1: tk_hb_eidan03_mot;
default: tk_hb_eidan03_wag;
}
switch(FEAT_TRAINS, PARENT, sw_tk_hb_eidan03_mot_def_main_gfx,vehicle_is_reversed){
    1: sw_tk_hb_eidan03_mot_def_rev_gfx;
    sw_tk_hb_eidan03_mot_def_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_mot_8_gfx,position_in_consist %8){
    0: tk_hb_eidan03_front;
    1: tk_hb_eidan03_mot;
    2: tk_hb_eidan03_wag;
    3: tk_hb_eidan03_wag;
    4: tk_hb_eidan03_wag;
    5: tk_hb_eidan03_mot;
    6: tk_hb_eidan03_wag;
    7: tk_hb_eidan03_end;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_mot_8_rev_gfx,position_in_consist %8){
    0: tk_hb_eidan03_front;
    1: tk_hb_eidan03_wag;
    2: tk_hb_eidan03_mot;
    3: tk_hb_eidan03_wag;
    4: tk_hb_eidan03_wag;
    5: tk_hb_eidan03_wag;
    6: tk_hb_eidan03_mot;
    7: tk_hb_eidan03_end;
}
switch(FEAT_TRAINS, PARENT, sw_tk_hb_eidan03_mot_8_main_gfx,vehicle_is_reversed){
    1: sw_tk_hb_eidan03_mot_8_rev_gfx;
    sw_tk_hb_eidan03_mot_8_gfx;
}
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_end_gfx,position_in_consist_from_end){
    0: tk_hb_eidan03_end;
    default: sw_tk_hb_eidan03_mot_def_main_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_front_gfx,position_in_consist){
    0: tk_hb_eidan03_front;
    default: sw_tk_hb_eidan03_end_gfx;
}
//main gfx 
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_livery_1,num_vehs_in_consist){
8: sw_tk_hb_eidan03_mot_8_main_gfx;
16: sw_tk_hb_eidan03_mot_8_main_gfx;
default: sw_tk_hb_eidan03_front_gfx;
}
//*livery 2
//special checkers
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_wag_pos_check_gfx,position_in_consist){
4: tk_hb_eidan03_1_mot;
tk_hb_eidan03_wag;
}
//setup
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_wag_gfx,position_in_articulated_veh){
    1: sw_tk_hb_eidan03_1_wag_pos_check_gfx;
    default: tk_hb_eidan03_wag;
}
//panto-placers
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_pos_check_0_gfx,position_in_consist_from_end){
1: tk_hb_eidan03_1_mot;
tk_hb_eidan03_mot;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_pos_check_gfx,position_in_consist){
1: tk_hb_eidan03_1_mot;
sw_tk_hb_eidan03_1_mot_pos_check_0_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_pos_check_0_rev_gfx,position_in_consist_from_end){
1: tk_hb_eidan03_1_mot;
tk_hb_eidan03_mot;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_pos_check_rev_gfx,position_in_consist){
1: tk_hb_eidan03_1_mot;
sw_tk_hb_eidan03_1_mot_pos_check_0_rev_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_def_gfx,position_in_consist %3){
1: sw_tk_hb_eidan03_1_mot_pos_check_gfx;
default: sw_tk_hb_eidan03_1_wag_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_def_rev_gfx,position_in_consist_from_end %3){
1: sw_tk_hb_eidan03_1_mot_pos_check_rev_gfx;
default: sw_tk_hb_eidan03_1_wag_gfx;
}
switch(FEAT_TRAINS, PARENT, sw_tk_hb_eidan03_1_mot_def_main_gfx,vehicle_is_reversed){
    1: sw_tk_hb_eidan03_1_mot_def_rev_gfx;
    sw_tk_hb_eidan03_1_mot_def_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_8_gfx,position_in_consist %8){
    0: tk_hb_eidan03_1_front;
    1: tk_hb_eidan03_1_mot;
    2: tk_hb_eidan03_wag;
    3: tk_hb_eidan03_wag;
    4: tk_hb_eidan03_wag;
    5: tk_hb_eidan03_wag;
    6: tk_hb_eidan03_1_mot;
    7: tk_hb_eidan03_1_end;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_mot_8_rev_gfx,position_in_consist %8){
    0: tk_hb_eidan03_1_front;
    1: tk_hb_eidan03_1_wag;
    2: tk_hb_eidan03_mot;
    3: tk_hb_eidan03_wag;
    4: tk_hb_eidan03_wag;
    5: tk_hb_eidan03_wag;
    6: tk_hb_eidan03_1_mot;
    7: tk_hb_eidan03_1_end;
}
switch(FEAT_TRAINS, PARENT, sw_tk_hb_eidan03_1_mot_8_main_gfx,vehicle_is_reversed){
    1: sw_tk_hb_eidan03_1_mot_8_rev_gfx;
    sw_tk_hb_eidan03_1_mot_8_gfx;
}
//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_end_gfx,position_in_consist_from_end){
    0: tk_hb_eidan03_1_end;
    default: sw_tk_hb_eidan03_1_mot_def_main_gfx;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_1_front_gfx,position_in_consist){
    0: tk_hb_eidan03_1_front;
    default: sw_tk_hb_eidan03_1_end_gfx;
}
//main gfx 
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_livery_2,num_vehs_in_consist){
8: sw_tk_hb_eidan03_1_mot_8_main_gfx;
16: sw_tk_hb_eidan03_1_mot_8_main_gfx;
default: sw_tk_hb_eidan03_1_front_gfx;
}
/*-----------------------*/
//livery randomizer 
random_switch(FEAT_TRAINS, PARENT, sw_tk_hb_eidan03_livery_0){
    3: sw_tk_hb_eidan03_livery_1;
    1: sw_tk_hb_eidan03_livery_2;
}
switch(FEAT_TRAINS, SELF, tk_hb_eidan03_date, build_year>1990){
  1: sw_tk_hb_eidan03_livery_0;
  default: sw_tk_hb_eidan03_livery_1;
}
//livery selector 
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_lv, cargo_subtype){
    0: tk_hb_eidan03_date;
    1: sw_tk_hb_eidan03_livery_1;
    2: sw_tk_hb_eidan03_livery_2;
}
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_lv_desc, cargo_subtype){
    0: string(LV_RANDOM);
    1: string(LV_tk_hn_eidan03_1);
    2: string(LV_tk_hn_eidan03_2);
}
//loading speed
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_loading, cargo_subtype){
    0: param_loading_3D;
    1: param_loading_3D;
    2: param_loading_5D;
}
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tk_hb_eidan03_main_capacity,position_in_consist_from_end){
    0: 132*param_capacity_mod/3;
    default: 138*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_hb_eidan03_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}


item(FEAT_TRAINS, tk_hb_eidan03,1031) {
    property {
        name: string(STR_tk_hn_eidan03_NAME);
        introduction_date: date(1988, 1, 1);
        model_life: 32;
        vehicle_life: 33;

        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 300 kW;
        weight: 33 ton;

        cargo_capacity: 132;
        loading_speed: param_loading_3D;

        reliability_decay: 20;
        cost_factor: 20;
        running_cost_factor: 50;
        
        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_tk_hb_eidan03_lv;
        purchase: tk_hb_eidan03_purchase;
        can_attach_wagon: tk_hb_eidan03_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_8_CARS),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_TOKYOMETRO,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_HIBIYA)
                            ),
                        );
        cargo_subtype_text:           sw_tk_hb_eidan03_lv_desc;
        length: 8;
        start_stop: sw_stop_start_2;
        cargo_capacity: 132*param_capacity_mod/3;
    }
    livery_override(mu_car_narrow){
        default:                      sw_tk_hb_eidan03_lv;
        cargo_capacity:               sw_tk_hb_eidan03_main_capacity;
        loading_speed:                sw_tk_hb_eidan03_loading;
        
        power: 403;
        weight: 31;
        length: 8;
      }
}