spriteset(tk_hz_8000series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/8000series/8000series_purchase.png"){ tmpl_purchase(0, 0) }


spriteset(tk_hz_8000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/8000series/8000series.png"){ tmpl_std(0, 0) }
spriteset(tk_hz_8000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/8000series/8000series.png"){ tmpl_std_rev(0, 25) }
spriteset(tk_hz_8000series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/8000series/8000series.png"){ tmpl_symm(0, 50) }
spriteset(tk_hz_8000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/8000series/8000series.png"){ tmpl_symm(0, 75) }

//*livery 1
  //panto-placers
    //var 1
      switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_panto_placer_1,position_in_vehid_chain %6){
          0: tk_hz_8000series_mot;
          2: tk_hz_8000series_mot;
          tk_hz_8000series_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_panto_placer_1_rev,position_in_vehid_chain_from_end %6 -1){
          0: tk_hz_8000series_mot;
          2: tk_hz_8000series_mot;
          tk_hz_8000series_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_tk_hz_8000series_panto_placer_main_1,vehicle_is_reversed){
          1: sw_tk_hz_8000series_panto_placer_1_rev;
          sw_tk_hz_8000series_panto_placer_1;
      }
    //var 2
      switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_panto_placer_2,position_in_vehid_chain %6){
          0: tk_hz_8000series_mot;
          4: tk_hz_8000series_mot;
          tk_hz_8000series_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_panto_placer_2_rev,position_in_vehid_chain_from_end %6 -1){
          0: tk_hz_8000series_mot;
          4: tk_hz_8000series_mot;
          tk_hz_8000series_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_tk_hz_8000series_panto_placer_main_2,vehicle_is_reversed){
          1: sw_tk_hz_8000series_panto_placer_2_rev;
          sw_tk_hz_8000series_panto_placer_2;
      }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_panto_placer_main,num_vehs_in_vehid_chain+1){
        7..8: sw_tk_hz_8000series_panto_placer_main_2;
        sw_tk_hz_8000series_panto_placer_main_1;
    }
  //front-end-fixer
  switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_gfx_main,position_in_vehid_chain_from_end){
      0: tk_hz_8000series_end;
      default: sw_tk_hz_8000series_panto_placer_main;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_lv,vehicle_type_id){
      tk_hz_8000series: tk_hz_8000series_front;
      default: sw_tk_hz_8000series_gfx_main;
  }
//post fixers
  //capacity-changer
  switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_capacity,position_in_vehid_chain_from_end){
    0: 136;
    default: 144;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_capacity_1,position_in_vehid_chain){
    0: 136;
    default: sw_tk_hz_8000series_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_capacity_main,vehicle_type_id){
    mu_car_narrow: sw_tk_hz_8000series_capacity;
    sw_tk_hz_8000series_capacity_1;
  }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_power_1,position_in_vehid_chain_from_end%11){
      1..2:return 160;
      5..8:return 160;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_head_power_1,num_vehs_in_consist){
      39: return 160;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_power_main_1,vehicle_type_id){
      mu_car_narrow:sw_tk_hz_8000series_power_1;
      sw_tk_hz_8000series_head_power_1;
    }
    //2004 VVVF
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_power_2,position_in_vehid_chain_from_end%11){
      1..2:return 165;
      5..6:return 165;
      8:return 165;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_head_power_2,num_vehs_in_consist){
      39: return 165;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_power_main_2,vehicle_type_id){
      mu_car_narrow:sw_tk_hz_8000series_power_2;
      sw_tk_hz_8000series_head_power_2;
    }
    //main
    switch(FEAT_TRAINS, SELF, sw_tk_hz_8000series_power_main,build_year>2003){
      1: sw_tk_hz_8000series_power_main_2;
      sw_tk_hz_8000series_power_main_1;
    }
    //attachable vehicles
    switch(FEAT_TRAINS, SELF, tk_hz_8000series_attach_vehid, vehicle_type_id) {
      mu_car: return string(STR_ATTACH_1067_ERROR);
      mu_car_metro: return string(STR_ATTACH_1067_ERROR);
      return CB_RESULT_ATTACH_ALLOW;
    }
//
item(FEAT_TRAINS, tk_hz_8000series,1100) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_EIDAN), "8000", string(STR_NAME_SERIES));
        introduction_date: date(1980, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 136;
        loading_speed: param_loading_4D;
        cost_factor: 19;
        running_cost_factor: 49;

        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 384 kW;
        weight: 32 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        power: sw_tk_hz_8000series_power_main()*4*1342/1000;
        default: sw_tk_hz_8000series_lv;
        purchase: tk_hz_8000series_purchase;
        can_attach_wagon: tk_hz_8000series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_1, 10),
                            string(STR_OPERATOR, 
                            string(STR_TOKYOMETRO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_HANZO)
                            ),
                        );
        start_stop: sw_stop_start_3;   
        cargo_capacity: sw_tk_hz_8000series_capacity_main()*param_capacity_mod/3;
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tk_hz_8000series_capacity_main()*param_capacity_mod/3;
        default:                      sw_tk_hz_8000series_lv;
        power: sw_tk_hz_8000series_power_main()*4*1342/1000;
        weight: 31;
        length: 8;
      }
}