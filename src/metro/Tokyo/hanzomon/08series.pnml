spriteset (tk_hz_08series_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
alternative_sprites(tk_hz_08series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series_purchase.png"){ tmpl_purchase(0, 0) }

spriteset (tk_hz_08series_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_hz_08series_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
spriteset (tk_hz_08series_mot_2,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_mot_rev,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_hz_08series_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }

alternative_sprites(tk_hz_08series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_high_skirt(0, 0) }
alternative_sprites(tk_hz_08series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_high_skirt_rev(0, 25) }
alternative_sprites(tk_hz_08series_mot_2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_symm(0, 50) }
alternative_sprites(tk_hz_08series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_symm(0, 75) }
alternative_sprites(tk_hz_08series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std(0, 100) }
alternative_sprites(tk_hz_08series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hanzomon/08series/08series.png"){ tmpl_std_rev(0, 100) }

//*livery 1
  //panto-placers
    //var 1
      switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_panto_placer_1,position_in_vehid_chain %6){
          0: tk_hz_08series_mot_2;
          3: tk_hz_08series_mot;
          tk_hz_08series_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_panto_placer_1_rev,position_in_vehid_chain_from_end %6 -1){
          0: tk_hz_08series_mot_2;
          3: tk_hz_08series_mot_rev;
          tk_hz_08series_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_tk_hz_08series_panto_placer_main_1,vehicle_is_reversed){
          1: sw_tk_hz_08series_panto_placer_1_rev;
          sw_tk_hz_08series_panto_placer_1;
      }
    //var 2
      switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_panto_placer_2,position_in_vehid_chain %6){
          0: tk_hz_08series_mot_2;
          4: tk_hz_08series_mot_2;
          tk_hz_08series_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_panto_placer_2_rev,position_in_vehid_chain_from_end %6 -1){
          0: tk_hz_08series_mot_2;
          4: tk_hz_08series_mot_2;
          tk_hz_08series_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_tk_hz_08series_panto_placer_main_2,vehicle_is_reversed){
          1: sw_tk_hz_08series_panto_placer_2_rev;
          sw_tk_hz_08series_panto_placer_2;
      }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_panto_placer_main,num_vehs_in_vehid_chain+1){
        7..8: sw_tk_hz_08series_panto_placer_main_2;
        sw_tk_hz_08series_panto_placer_main_1;
    }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_gfx_main,position_in_vehid_chain_from_end){
        0: tk_hz_08series_end;
        default: sw_tk_hz_08series_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_lv,vehicle_type_id){
        tk_hz_08series: tk_hz_08series_front;
        default: sw_tk_hz_08series_gfx_main;
    }
//post fixers
  //capacity-changer
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_capacity,position_in_vehid_chain_from_end){
      0: 141;
      default: 152;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_capacity_1,position_in_vehid_chain){
      0: 141;
      default: sw_tk_hz_08series_capacity;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_capacity_main,vehicle_type_id){
      mu_car_narrow: sw_tk_hz_08series_capacity;
      sw_tk_hz_08series_capacity_1;
    }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_power,position_in_vehid_chain_from_end%11){
      1..2:return 165;
      5:return 165;
      7..8:return 165;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_head_power,num_vehs_in_consist){
      39: return 165;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hz_08series_power_main,vehicle_type_id){
        mu_car_narrow:sw_tk_hz_08series_power;
        sw_tk_hz_08series_head_power;
    }
  //attachable vehicles
    switch(FEAT_TRAINS, SELF, tk_hz_08series_attach_vehid, vehicle_type_id) {
      mu_car: return string(STR_ATTACH_1067_ERROR);
      mu_car_metro: return string(STR_ATTACH_1067_ERROR);
      return CB_RESULT_ATTACH_ALLOW;
    }
//
item(FEAT_TRAINS, tk_hz_08series,1101) {
    property {
        name: string(STR_tk_hz_08series_NAME);
        introduction_date: date(2002, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 20;

        cargo_capacity: 141;
        loading_speed: param_loading_4D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 660 kW;
        weight: 27 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        power: sw_tk_hz_08series_power_main()*4*1342/1000;
        default: sw_tk_hz_08series_lv;
        purchase: tk_hz_08series_purchase;
        can_attach_wagon: tk_hz_08series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_10_CARS),
                            string(STR_CARS),
                            string(STR_OPERATOR, 
                            string(STR_TOKYOMETRO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_HANZO)
                            ),
                        );
        start_stop: sw_stop_start_3;   
        cargo_capacity: sw_tk_hz_08series_capacity_main()*param_capacity_mod/3;
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tk_hz_08series_capacity_main()*param_capacity_mod/3;
        default:                      sw_tk_hz_08series_lv;
        power: sw_tk_hz_08series_power_main()*4*1342/1000;
        weight: 28;
        length: 8;
      }
}