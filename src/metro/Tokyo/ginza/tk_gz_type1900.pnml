spriteset (tk_gz_type1900_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_gz_type1900_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_gz_type1900_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_gz_type1900_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 50) }
spriteset (tk_gz_type2000_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_gz_type2000_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_gz_type2000_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 50) }
alternative_sprites(tk_gz_type1900_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_gz_type1900_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_gz_type1900_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900.png"){ tmpl_std(0, 25) }
alternative_sprites(tk_gz_type1900_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900.png"){ tmpl_std_rev(0, 50) }
alternative_sprites(tk_gz_type2000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900.png"){ tmpl_std(0, 75) }
alternative_sprites(tk_gz_type2000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900.png"){ tmpl_std(0, 100) }
alternative_sprites(tk_gz_type2000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1900/type1900.png"){ tmpl_std_rev(0, 125) }
//setup
//basic gfx
// livery 1
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1900_end_gfx,position_in_consist_from_end){
    0: tk_gz_type1900_end;
    tk_gz_type1900_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1900_livery_1,position_in_consist){
    0: tk_gz_type1900_front;
    sw_tk_gz_t1900_end_gfx;
}
// livery 2
switch(FEAT_TRAINS, SELF, sw_tk_gz_t2000_end_gfx,position_in_consist_from_end){
    0: tk_gz_type2000_end;
    tk_gz_type2000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1900_livery_2,position_in_consist){
    0: tk_gz_type2000_front;
    sw_tk_gz_t2000_end_gfx;
}
//livery-randomizer
random_switch(FEAT_TRAINS, PARENT, sw_tk_gz_t1900_livery_0){
    3: sw_tk_gz_t1900_livery_1;
    1: sw_tk_gz_t1900_livery_2;
}
//livery selector
switch(FEAT_TRAINS, PARENT, sw_tk_gz_t1900_lv, cargo_subtype){
    0: sw_tk_gz_t1900_livery_0;
    1: sw_tk_gz_t1900_livery_1;
    2: sw_tk_gz_t1900_livery_2;
}
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1900_lv_desc, cargo_subtype){
    0: string(LV_RANDOM);
    1: string(LV_tk_gz_type1900_1);
    2: string(LV_tk_gz_type1900_2);
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1900_attach_vehid, vehicle_type_id) {
  mu_car_metro: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, tk_gz_t1900, 1004) {
    property {
        name: string(STR_NAME_DENSHA, string(STR_NAME_EIDAN), "1900", string(STR_NAME_TYPE));
        introduction_date: date(1959, 1, 1);
        model_life: 29;
        vehicle_life: 34;
        reliability_decay:23;

        cargo_capacity: 95;
        loading_speed: param_loading_3D;
        cost_factor: 17;
        running_cost_factor: 47;

        speed: 70 km/h;
        track_type: TR_1435_M;
        power: 220 kW;
        weight: 29 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_tk_gz_t1900_lv;
        purchase: tk_gz_type1900_purchase;
        can_attach_wagon: sw_tk_gz_t1900_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_2, 3,6),
                            string(STR_OPERATOR, 
                            string(STR_TOKYOMETRO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_GINZA)
                            ),
                        );
        cargo_subtype_text: sw_tk_gz_t1900_lv_desc;
        length: 8;
        cargo_capacity: 95*param_capacity_mod/3;
    }
    livery_override(mu_car_metro){
        cargo_subtype_text: sw_tk_gz_t1900_lv_desc;
        default:                      sw_tk_gz_t1900_lv;
        cargo_capacity:               95*param_capacity_mod/3;
        loading_speed:                param_loading_3D;
        power: 550;
        weight: 24;
        length: 8;
      }
}
