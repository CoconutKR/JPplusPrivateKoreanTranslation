spriteset (tk_gz_1000series_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_gz_1000series_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_gz_1000series_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
spriteset (tk_gz_1000series_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_gz_1000series_front_f,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_gz_1000series_end_f,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 50) }

alternative_sprites(tk_gz_1000series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/1000series/1000series_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_gz_1000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/1000series/1000series.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_gz_1000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/1000series/1000series.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tk_gz_1000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/1000series/1000series.png"){ tmpl_symm(0, 50) }
alternative_sprites(tk_gz_1000series_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/1000series/1000series.png"){ tmpl_std(0, 75) }
alternative_sprites(tk_gz_1000series_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/1000series/1000series.png"){ tmpl_std_rev(0, 75) }

//*livery 1
  //fake head-end placer
    switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_fake_placer,position_in_vehid_chain +1){
      5: tk_gz_1000series_end_f;
      6: tk_gz_1000series_front_f;
      tk_gz_1000series_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_fake_placer_rev,position_in_vehid_chain_from_end ){
      5: tk_gz_1000series_front_f;
      6: tk_gz_1000series_end_f;
      tk_gz_1000series_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_gz_1000series_fake_placer_gfx,vehicle_is_reversed){
      1: sw_tk_gz_1000series_fake_placer_rev;
      sw_tk_gz_1000series_fake_placer;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_fake_placer_main,num_vehs_in_vehid_chain>10 - 1){
      1: sw_tk_gz_1000series_fake_placer_gfx;
      tk_gz_1000series_wag;
    }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_gfx_main,position_in_vehid_chain_from_end){
      0: tk_gz_1000series_end;
      default: sw_tk_gz_1000series_fake_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_lv,vehicle_type_id){
      tk_gz_1000s: tk_gz_1000series_front;
      default: sw_tk_gz_1000series_gfx_main;
    }
//post fixers
  //capacity-changer
  switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_capacity,position_in_vehid_chain_from_end){
    0: 93;
    default: 106;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_capacity_1,position_in_vehid_chain){
    0: 93;
    default: sw_tk_gz_1000series_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_gz_1000series_capacity_main,vehicle_type_id){
    mu_car_metro: sw_tk_gz_1000series_capacity;
    sw_tk_gz_1000series_capacity_1;
  }
//attachable vehicles
  switch(FEAT_TRAINS, SELF, sw_tk_gz_1000s_attach_vehid, vehicle_type_id) {
    mu_car: return string(STR_ATTACH_1435_M_ERROR);
    mu_car_narrow: return string(STR_ATTACH_1435_M_ERROR);
    return CB_RESULT_ATTACH_ALLOW;
  }

item(FEAT_TRAINS, tk_gz_1000s, 1006) {
    property {
        name: string(STR_tk_gz_1000series_NAME);
        introduction_date: date(2011, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 20;

        cargo_capacity: 93;
        loading_speed: param_loading_3D;
        cost_factor: 18;
        running_cost_factor: 48;

        speed: 90 km/h;
        track_type: TR_1435_M;
        power: 480 kW;
        weight: 30 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_tk_gz_1000series_lv;
        purchase: tk_gz_1000series_purchase;
        can_attach_wagon: sw_tk_gz_1000s_attach_vehid;
    additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_6_CARS),
                            string(STR_CARS),
                            string(STR_OPERATOR, 
                            string(STR_TOKYOMETRO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_GINZA)
                            ),
                        );
        length: 8;
        start_stop: sw_stop_start_3;
        cargo_capacity: sw_tk_gz_1000series_capacity_main()*param_capacity_mod/3;
    }
    livery_override(mu_car_metro){
        default:                      sw_tk_gz_1000series_lv;
        cargo_capacity: sw_tk_gz_1000series_capacity_main()*param_capacity_mod/3;
        loading_speed:                param_loading_3D;
        power: 480*1342/1000;
        weight: 27;
        length: 8;
      }
}