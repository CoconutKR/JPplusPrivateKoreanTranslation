//purchase
spriteset(tk_gz_eidan01_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/eidan01/eidan01.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_gz_eidan01_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/eidan01/eidan01.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_4(tk_gz_eidan01_purchase, logo_tokyo_metro, logo_line_tk_ginza, tk_gz_eidan01_purchase_c1, tk_gz_eidan01_purchase_c2)

spriteset(tk_gz_eidan01_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/eidan01/eidan01.png"){ tmpl_std(0, 0) }
spriteset(tk_gz_eidan01_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/eidan01/eidan01.png"){ tmpl_std_rev(0, 25) }
spriteset(tk_gz_eidan01_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/eidan01/eidan01.png"){ tmpl_symm(0, 50) }
//*livery 1
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_gfx_main,position_in_vehid_chain_from_end){
      0: tk_gz_eidan01_end;
      default: tk_gz_eidan01_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_lv,vehicle_type_id){
      tk_gz_eidan01: tk_gz_eidan01_front;
      default: sw_tk_gz_eidan01_gfx_main;
    }
//post fixers
  //capacity-changer
  switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_capacity,position_in_vehid_chain_from_end){
    0: 100;
    default: 102;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_capacity_1,position_in_vehid_chain){
    0: 100;
    default: sw_tk_gz_eidan01_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_capacity_main,vehicle_type_id){
    mu_car_metro: sw_tk_gz_eidan01_capacity;
    sw_tk_gz_eidan01_capacity_1;
  }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_power,position_in_vehid_chain_from_end%5){
      1..2:return 120;
      4:return 120;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_head_power,num_vehs_in_consist){
      39: return 120;
      return 0;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_gz_eidan01_power_main,vehicle_type_id){
      mu_car_metro:sw_tk_gz_eidan01_power;
      sw_tk_gz_eidan01_head_power;
    }
  //attachable vehicles
    switch(FEAT_TRAINS, SELF, sw_tk_gz_01s_attach_vehid, vehicle_type_id) {
      mu_car: return string(STR_ATTACH_1435_M_ERROR);
      mu_car_narrow: return string(STR_ATTACH_1435_M_ERROR);
      return CB_RESULT_ATTACH_ALLOW;
    }

item(FEAT_TRAINS, tk_gz_eidan01, 1005) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_EIDAN), "01", string(STR_NAME_SERIES));
        introduction_date: date(1983, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay:21;

        cargo_capacity: 100;
        loading_speed: param_loading_3D;
        cost_factor: 18;
        running_cost_factor: 18;

        speed: 80 km/h;
        track_type: TR_1435_M;
        power: 240 kW;
        weight: 29 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        //length: 8;
    }
	graphics {
        power: sw_tk_gz_eidan01_power_main()*4*1342/1000;
        default: sw_tk_gz_eidan01_lv;
        purchase: tk_gz_eidan01_purchase;
        can_attach_wagon: sw_tk_gz_01s_attach_vehid;
        additional_text:
    string(STR_TRAIN_DESC,
		string(STR_STANDARDGAUGE),
		string(STR_DENKI, string(STR_DC_N, 600)),
		string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
		string(STR_FORMATION, string(STR_NUM_CARS_1, 6)),
		string(STR_OPERATOR, string(STR_TOKYOMETRO)),
		string(STR_LINES_USED, string(STR_TOKYO_GINZA)));

        length: 8;
        start_stop: sw_stop_start_3;
        cargo_capacity: sw_tk_gz_eidan01_capacity_main()*param_capacity_mod/3;
    }
	livery_override(mu_car_metro){
		cargo_subtype_text:sw_empty_desc;
        default: sw_tk_gz_eidan01_lv;
        cargo_capacity: sw_tk_gz_eidan01_capacity_main()*param_capacity_mod/3;
        loading_speed:                param_loading_3D;
        power: sw_tk_gz_eidan01_power_main()*4*1342/1000;
        weight: 24;
        length: 8;
      }
}