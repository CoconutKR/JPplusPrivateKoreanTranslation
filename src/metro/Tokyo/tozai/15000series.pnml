spriteset(tk_tz_15000series_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series_purchase.png"){ tmpl_purchase(0, 0) }


spriteset(tk_tz_15000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series.png"){ tmpl_high_skirt(0, 0) }
spriteset(tk_tz_15000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series.png"){ tmpl_high_skirt_rev(0, 25) }
spriteset(tk_tz_15000series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series.png"){ tmpl_std(0, 50) }
spriteset(tk_tz_15000series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series.png"){ tmpl_std_rev(0, 50) }
spriteset(tk_tz_15000series_mot_2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series.png"){ tmpl_symm(0, 75) }
spriteset(tk_tz_15000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/tozai/15000series/15000series.png"){ tmpl_symm(0, 100) }

//*livery 1
	//panto-placers
		//var 1
		switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_panto_placer_1,position_in_vehid_chain %6){
      0: tk_tz_15000series_mot_2;
      3: tk_tz_15000series_mot;
      tk_tz_15000series_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_panto_placer_1_rev,position_in_vehid_chain_from_end %6 -1){
      0: tk_tz_15000series_mot_2;
      3: tk_tz_15000series_mot_rev;
      tk_tz_15000series_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_tz_15000series_panto_placer_main_1,vehicle_is_reversed){
      1: sw_tk_tz_15000series_panto_placer_1_rev;
      sw_tk_tz_15000series_panto_placer_1;
    }
		//var 2
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_panto_placer_2,position_in_vehid_chain %4){
      1: tk_tz_15000series_mot_2;
      tk_tz_15000series_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_panto_placer_2_rev,position_in_vehid_chain_from_end %4 -1){
      1: tk_tz_15000series_mot_2;
      tk_tz_15000series_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_tz_15000series_panto_placer_main_2,vehicle_is_reversed){
      1: sw_tk_tz_15000series_panto_placer_2_rev;
      sw_tk_tz_15000series_panto_placer_2;
    }
		//controller 
		switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_panto_placer_main,num_vehs_in_vehid_chain+1){
      5: sw_tk_tz_15000series_panto_placer_main_2;
      8: sw_tk_tz_15000series_panto_placer_main_2;
      sw_tk_tz_15000series_panto_placer_main_1;
    }
	//front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_gfx_main,position_in_vehid_chain_from_end){
      0: tk_tz_15000series_end;
      default: sw_tk_tz_15000series_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_lv,vehicle_type_id){
      tk_tz_15000series: tk_tz_15000series_front;
      default: sw_tk_tz_15000series_gfx_main;
    }
//*post fixers
  //capacity-changer
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_capacity,position_in_vehid_chain_from_end){
      0: 143;
			1: 155;
			8: 155;
      154;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_capacity_1,position_in_vehid_chain){
      0: 143;
      default: sw_tk_tz_15000series_capacity;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_capacity_main,vehicle_type_id){
      mu_car_narrow: sw_tk_tz_15000series_capacity;
      sw_tk_tz_15000series_capacity_1;
    }
	//power
		switch(FEAT_TRAINS, SELF, sw_tk_tz_15000series_power_main,position_in_vehid_chain_from_end%10){
      1..2:return 225;
      5:return 225;
      7..8:return 225;
      return 0;
    }
	//attachable vehicles
	switch(FEAT_TRAINS, SELF, tk_tz_15000series_attach_vehid, vehicle_type_id) {
 		mu_car: return string(STR_ATTACH_1067_ERROR);
		mu_car_metro: return string(STR_ATTACH_1067_ERROR);
		return CB_RESULT_ATTACH_ALLOW;
	}
//
item(FEAT_TRAINS, tk_tz_15000series,1043) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_TOKYO_METRO), "15000", string(STR_NAME_SERIES));
        introduction_date: date(2010, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 143*param_capacity_mod/3;
        loading_speed: param_loading_4D;
        cost_factor: 22;
        running_cost_factor: 52;

        speed: 120 km/h;
        track_type: TR_1067_DC;
        power: 900 kW;
        weight: 34 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.15000;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        power: sw_tk_tz_15000series_power_main()*4*1342/1000;
        default: sw_tk_tz_15000series_lv;
        purchase: tk_tz_15000series_purchase;
        can_attach_wagon: tk_tz_15000series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_NARROWGAUGE),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_1, 10),
                            string(STR_OPERATOR, 
                            string(STR_TOKYOMETRO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_TOZAI)
                            ),
                        );
        start_stop: sw_stop_start_3;  
        cargo_capacity: sw_tk_tz_15000series_capacity_main()*param_capacity_mod/3;
    }
    livery_override(mu_car_narrow){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tk_tz_15000series_capacity_main()*param_capacity_mod/3;
        default: sw_tk_tz_15000series_lv;
        power: sw_tk_tz_15000series_power_main()*4*1342/1000;
        weight: 25;
        length: 8;
      }
}
