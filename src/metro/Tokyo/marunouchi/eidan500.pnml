spriteset (tk_mn_eidan500_purchase,   "gfx/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_mn_eidan500_front,       "gfx/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_mn_eidan500_wag,       "gfx/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_mn_eidan500_end,       "gfx/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
alternative_sprites(tk_mn_eidan500_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/marunouchi/eidan500/eidan500_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_mn_eidan500_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/marunouchi/eidan500/eidan500.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_mn_eidan500_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/marunouchi/eidan500/eidan500.png"){ tmpl_std(0, 50) }
alternative_sprites(tk_mn_eidan500_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/marunouchi/eidan500/eidan500.png"){ tmpl_std_rev(0, 25) }

//setup
//basic gfx
// livery 1
switch(FEAT_TRAINS, SELF, sw_tk_mn_eidan500_end_gfx,position_in_consist_from_end){
    0: tk_mn_eidan500_end;
    tk_mn_eidan500_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_mn_eidan500_livery_1,position_in_consist){
    0: tk_mn_eidan500_front;
    sw_tk_mn_eidan500_end_gfx;
}
//capacity changer
switch(FEAT_TRAINS, SELF, sw_tk_mn_eidan500_main_capacity,position_in_consist_from_end){
    0: 122*param_capacity_mod/3;
    default: 137*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, sw_tk_mn_eidan500_attach_vehid, vehicle_type_id) {
  mu_car_metro: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, eidan500,1021) {
    property {
        name: string(STR_tk_mn_eidan500_NAME);
        introduction_date: date(1953, 1, 1);
        model_life: 40;
        vehicle_life: 30;
        reliability_decay: 22;

        cargo_capacity: 122;
        loading_speed: param_loading_3D;
        cost_factor: 18;
        running_cost_factor: 48;

        speed: 70 km/h;
        track_type: TR_1435_M;
        power: 300 kW;
        weight: 35 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_tk_mn_eidan500_livery_1;
        purchase: tk_mn_eidan500_purchase;
        can_attach_wagon: sw_tk_mn_eidan500_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_tk_mn_eidan500_DESC),
                            string(STR_CARS),
                            string(STR_CITY,
                            string(STR_TOKYOMETRO,
                            string(STR_OPERATOR)
                            ),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_MARU)
                            ),
                        );
        length: 8;
        start_stop: sw_stop_start_2;
        cargo_capacity: 122*param_capacity_mod/3;
    }
    livery_override(mu_car_metro){
        default:                      sw_tk_mn_eidan500_livery_1;
        cargo_capacity:               sw_tk_mn_eidan500_main_capacity;
        loading_speed:                param_loading_3D;
        power: 402;
        weight: 30;
        length: 8;
      }
}