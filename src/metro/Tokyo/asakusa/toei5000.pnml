//purchase
spriteset(tk_as_toei5000_purchase_c1_1,      "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_as_toei5000_purchase_c2_1,      "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png"){ tmpl_purchase_2c(0, 75) }
spriteset(tk_as_toei5000_purchase_c1_2,      "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_as_toei5000_purchase_c2_2,      "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png"){ tmpl_purchase_2c(0, 75) }
switch(FEAT_TRAINS, SELF, tk_as_toei5000_purchase_c1, current_year<1982){
			1: tk_as_toei5000_purchase_c1_1;
			tk_as_toei5000_purchase_c1_2;
	}
switch(FEAT_TRAINS, SELF, tk_as_toei5000_purchase_c2, current_year<1982){
			1: tk_as_toei5000_purchase_c2_1;
			tk_as_toei5000_purchase_c2_2;
	}
GRAPHIC_STACK_4(tk_as_toei5000_purchase, logo_toei, logo_line_tk_asakusa, tk_as_toei5000_purchase_c1, tk_as_toei5000_purchase_c2)

spriteset (tk_as_toei5000_front,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std(0, 0) }
spriteset (tk_as_toei5000_end,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std_rev(0, 25) }
spriteset (tk_as_toei5000_mot,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std(0, 75) }
spriteset (tk_as_toei5000_mot_rev,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std_rev(0, 75) }
spriteset (tk_as_toei5000_wag,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std(0, 50) }
spriteset (tk_as_toei5000_front_mot,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std(0,100) }
spriteset (tk_as_toei5000_end_mot,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000.png") { tmpl_std_rev(0, 125) }

spriteset (tk_as_toei5000_1_front,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std(0, 0)}
spriteset (tk_as_toei5000_1_end,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std_rev(0, 25) }
spriteset (tk_as_toei5000_1_mot,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std(0, 75) }
spriteset (tk_as_toei5000_1_mot_rev,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std_rev(0, 75) }
spriteset (tk_as_toei5000_1_wag,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std(0, 50) }
spriteset (tk_as_toei5000_1_front_mot,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std(0, 100) }
spriteset (tk_as_toei5000_1_end_mot,       "gfx/metro/Tokyo/asakusa/toei5000/toei5000_1.png") { tmpl_std_rev(0, 125) }

//*livery 1
	//special gfx
		//head gfx
		switch(FEAT_TRAINS, PARENT, sw_tk_as_toei5000_front_gfx,vehicle_is_reversed){
			1: tk_as_toei5000_front;
			tk_as_toei5000_front_mot;
		}
		switch(FEAT_TRAINS, PARENT, sw_tk_as_toei5000_end_gfx,vehicle_is_reversed){
			1: tk_as_toei5000_end_mot;
			tk_as_toei5000_end;
		}
	//panto-placers
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_panto_placer_1,position_in_vehid_chain %2){
			1: tk_as_toei5000_mot;
			tk_as_toei5000_wag;
		}
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_panto_placer_1_rev,1- position_in_vehid_chain_from_end %2){
			1: tk_as_toei5000_mot_rev;
			tk_as_toei5000_wag;
		}
		switch(FEAT_TRAINS, PARENT, sw_toei5000_panto_placer_main,vehicle_is_reversed){
			1: sw_tk_as_toei5000_panto_placer_1_rev;
			sw_tk_as_toei5000_panto_placer_1;
		}
	//front-end-fixer
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_gfx_main,position_in_vehid_chain_from_end){
			0: sw_tk_as_toei5000_end_gfx;
			default: sw_toei5000_panto_placer_main;
		}
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_livery_1,vehicle_type_id){
			tk_as_toei5000: sw_tk_as_toei5000_front_gfx;
			default: sw_tk_as_toei5000_gfx_main;
		}


//*livery 2
	//special gfx
		//head gfx
		switch(FEAT_TRAINS, PARENT, sw_tk_as_toei5000_1_front_gfx,vehicle_is_reversed){
			1: tk_as_toei5000_1_front;
			tk_as_toei5000_1_front_mot;
		}
		switch(FEAT_TRAINS, PARENT, sw_tk_as_toei5000_1_end_gfx,vehicle_is_reversed){
			1: tk_as_toei5000_1_end_mot;
			tk_as_toei5000_1_end;
		}
	//panto-placers
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_1_panto_placer_1,position_in_vehid_chain %2){
			1: tk_as_toei5000_1_mot;
			tk_as_toei5000_1_wag;
		}
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_1_panto_placer_1_rev,1- position_in_vehid_chain_from_end %2){
			1: tk_as_toei5000_1_mot_rev;
			tk_as_toei5000_1_wag;
		}
		switch(FEAT_TRAINS, PARENT, sw_toei5000_1_panto_placer_main,vehicle_is_reversed){
			1: sw_tk_as_toei5000_1_panto_placer_1_rev;
			sw_tk_as_toei5000_1_panto_placer_1;
		}
	//front-end-fixer
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_1_gfx_main,position_in_vehid_chain_from_end){
			0: sw_tk_as_toei5000_1_end_gfx;
			default: sw_toei5000_1_panto_placer_main;
		}
		switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_livery_2,vehicle_type_id){
			tk_as_toei5000: sw_tk_as_toei5000_1_front_gfx;
			default: sw_tk_as_toei5000_1_gfx_main;
		}


//livery controller
	//livery randomizer
	switch(FEAT_TRAINS, PARENT, sw_tk_as_toei5000_livery_0, current_year<1982){
			1: sw_tk_as_toei5000_livery_1;
			sw_tk_as_toei5000_livery_2;
	}
	//livery selector
	switch(FEAT_TRAINS, PARENT, sw_tk_as_toei5000_lv, cargo_subtype){
			0: sw_tk_as_toei5000_livery_0;
			1: sw_tk_as_toei5000_livery_1;
			2: sw_tk_as_toei5000_livery_2;
	}
	switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_lv_desc, cargo_subtype){
			0: string(LV_RANDOM);
			1: string(LV_OLD);
			2: string(LV_NEW);
	}
//post fixers
//capacity-changer
switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_capacity,position_in_vehid_chain_from_end){
    0: 140;
    default: 150;
}
switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_capacity_1,position_in_vehid_chain){
    0: 140;
    default: sw_tk_as_toei5000_capacity;
}
switch(FEAT_TRAINS, SELF, sw_tk_as_toei5000_capacity_main,vehicle_type_id){
    mu_car: sw_tk_as_toei5000_capacity;
    sw_tk_as_toei5000_capacity_1;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_as_toei5000_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return string(STR_ATTACH_1435_ERROR);
  mu_car_metro: return string(STR_ATTACH_1435_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tk_as_toei5000,1120) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_TOEI), "5000", string(STR_NAME_TYPE));
        introduction_date: date(1960, 1, 1);
        model_life: 30;
        vehicle_life: 35;
        reliability_decay: 10;

        cargo_capacity: 140;
        loading_speed: param_loading_3D;
        cost_factor: 19;
        running_cost_factor: 49;

        speed: 100 km/h;
        track_type: TR_1435_DC;
        power: 340 kW;
        weight: 34 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        default: sw_tk_as_toei5000_lv;
        purchase: tk_as_toei5000_purchase;
        can_attach_wagon: tk_as_toei5000_attach_vehid;
        additional_text:
    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION, string(STR_NUM_CARS_2, 4,6)),
                            string(STR_OPERATOR, 
                            string(STR_TOEI),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_ASAKUSA)
                            ),
                        );
        cargo_subtype_text: sw_tk_as_toei5000_lv_desc;
        start_stop: sw_stop_start_2; 
        cargo_capacity: sw_tk_as_toei5000_capacity_main()*param_capacity_mod/3;
				sound_effect: sw_TOEI5000_start_sound;
    }
    livery_override(mu_car){
        cargo_subtype_text: sw_tk_as_toei5000_lv_desc;
        loading_speed: param_loading_3D;
        cargo_capacity: sw_tk_as_toei5000_capacity_main()*param_capacity_mod/3;
        default:                      sw_tk_as_toei5000_lv;
        power: 456;
        weight: 33;
        length: 8;
      }
}