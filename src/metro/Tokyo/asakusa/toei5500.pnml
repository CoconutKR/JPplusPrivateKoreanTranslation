spriteset (tk_as_toei5500_purchase,   "gfx/misc/purchase.png") { tmpl_purchase(0, 0) }
spriteset (tk_as_toei5500_front,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 0) }
spriteset (tk_as_toei5500_mot,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_as_toei5500_wag,       "gfx/misc/gfx_placeholder.png") { tmpl_std(0, 25) }
spriteset (tk_as_toei5500_end,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
spriteset (tk_as_toei5500_front_f,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 50) }
spriteset (tk_as_toei5500_end_f,       "gfx/misc/gfx_placeholder.png") { tmpl_std_rev(0, 50) }

alternative_sprites(tk_as_toei5500_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500_purchase.png"){ tmpl_purchase(0, 0) }
alternative_sprites(tk_as_toei5500_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500.png"){ tmpl_std(0, 0) }
alternative_sprites(tk_as_toei5500_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500.png"){ tmpl_std_rev(0, 25) }
alternative_sprites(tk_as_toei5500_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500.png"){ tmpl_symm(0, 50) }
alternative_sprites(tk_as_toei5500_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500.png"){ tmpl_symm(0, 75) }
alternative_sprites(tk_as_toei5500_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500.png"){ tmpl_std(0, 100) }
alternative_sprites(tk_as_toei5500_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/asakusa/toei5500/toei5500.png"){ tmpl_std_rev(0, 100) }

//*livery 1
  //panto-placers
    //var 1
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_panto_placer_1,position_in_vehid_chain %8){
      1: tk_as_toei5500_mot;
      4: tk_as_toei5500_mot;
      default: tk_as_toei5500_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_panto_placer_1_rev,position_in_vehid_chain_from_end %8){
      2: tk_as_toei5500_mot;
      5: tk_as_toei5500_mot;
      default: tk_as_toei5500_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_toei5500_panto_placer_main_1,vehicle_is_reversed){
      1: sw_tk_as_toei5500_panto_placer_1_rev;
      sw_tk_as_toei5500_panto_placer_1;
    }
    //var 2
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_panto_placer_2,position_in_vehid_chain %3){
      0: tk_as_toei5500_mot;
      default: tk_as_toei5500_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_panto_placer_2_rev,1-position_in_vehid_chain_from_end %3){
      0: tk_as_toei5500_mot;
      default: tk_as_toei5500_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_toei5500_panto_placer_main_2,vehicle_is_reversed){
      1: sw_tk_as_toei5500_panto_placer_2_rev;
      sw_tk_as_toei5500_panto_placer_2;
    }
    //panto controller
    switch(FEAT_TRAINS, SELF, sw_toei5500_panto_placer_main,num_vehs_in_vehid_chain+1){
      3: sw_toei5500_panto_placer_main_2;
      6: sw_toei5500_panto_placer_main_2;
      10..11: sw_toei5500_panto_placer_main_2;
      sw_toei5500_panto_placer_main_1;
    }
  //fake head-end placer
		switch(FEAT_TRAINS, SELF, sw_toei5500_fake_placer,position_in_vehid_chain +1){
			7: tk_as_toei5500_end_f;
			8: tk_as_toei5500_front_f;
			sw_toei5500_panto_placer_main;
		}
		switch(FEAT_TRAINS, SELF, sw_toei5500_fake_placer_rev,position_in_vehid_chain_from_end ){
			7: tk_as_toei5500_front_f;
			8: tk_as_toei5500_end_f;
			sw_toei5500_panto_placer_main;
		}
		switch(FEAT_TRAINS, PARENT, sw_toei5500_fake_placer_gfx,vehicle_is_reversed){
			1: sw_toei5500_fake_placer_rev;
			sw_toei5500_fake_placer;
		}
		switch(FEAT_TRAINS, SELF, sw_toei5500_fake_placer_main,num_vehs_in_vehid_chain>11 - 1){
			1: sw_toei5500_fake_placer_gfx;
			sw_toei5500_panto_placer_main;
		}
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_gfx_main,position_in_vehid_chain_from_end){
        0: tk_as_toei5500_end;
        sw_toei5500_fake_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_livery_1,vehicle_type_id){
        mu_car: sw_tk_as_toei5500_gfx_main;
        tk_as_toei5500_front;
    }
//post fixers
    //capacity-changer
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_capacity,position_in_vehid_chain_from_end){
        0: 121;
        default: 134;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_capacity_1,position_in_vehid_chain){
        0: 121;
        default: sw_tk_as_toei5500_capacity;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_capacity_main,vehicle_type_id){
        mu_car: sw_tk_as_toei5500_capacity;
        sw_tk_as_toei5500_capacity_1;
    }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5500_power_main,position_in_vehid_chain_from_end%8){
      0..2:return 155;
      5..7:return 155;
      return 0;
    }
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_as_toei5500_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return string(STR_ATTACH_1435_ERROR);
  mu_car_metro: return string(STR_ATTACH_1435_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tk_as_toei5500,1123) {
    property {
        name: string(STR_tk_as_toei5500_NAME);
        introduction_date: date(2017, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 40;
        reliability_decay: 23;

        cargo_capacity: 121;
        loading_speed: param_loading_3D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 130 km/h;
        track_type: TR_1435_DC;
        power: 620 kW;
        weight: 30 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        power: sw_tk_as_toei5500_power_main()*4*1342/1000;
        default: sw_tk_as_toei5500_livery_1;
        purchase: tk_as_toei5500_purchase;
        can_attach_wagon: tk_as_toei5500_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_8_CARS),
                            string(STR_CARS),
                            string(STR_OPERATOR, 
                            string(STR_TOEI),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_ASAKUSA)
                            ),
                        );
        start_stop: sw_stop_start_4; 
        cargo_capacity: sw_tk_as_toei5500_capacity_main()*param_capacity_mod/3;
    }
    livery_override(mu_car){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_tk_as_toei5500_capacity_main()*param_capacity_mod/3;
        default:                      sw_tk_as_toei5500_livery_1;
        power: sw_tk_as_toei5500_power_main()*4*1342/1000;
        weight: 30;
        length: 8;
      }
}