//purchase
spriteset(tk_as_toei5300_purchase_c1,      "gfx/metro/Tokyo/asakusa/toei5300/toei5300.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_as_toei5300_purchase_c2,      "gfx/metro/Tokyo/asakusa/toei5300/toei5300.png"){ tmpl_purchase_2c(0, 75) }
GRAPHIC_STACK_4(tk_as_toei5300_purchase, logo_toei, logo_line_tk_asakusa, tk_as_toei5300_purchase_c1, tk_as_toei5300_purchase_c2)

spriteset (tk_as_toei5300_front,       "gfx/metro/Tokyo/asakusa/toei5300/toei5300.png") { tmpl_std(0, 0) }
spriteset (tk_as_toei5300_end,       "gfx/metro/Tokyo/asakusa/toei5300/toei5300.png") { tmpl_std_rev(0, 25) }
spriteset (tk_as_toei5300_mot,       "gfx/metro/Tokyo/asakusa/toei5300/toei5300.png") { tmpl_std(0, 50) }
spriteset (tk_as_toei5300_wag,       "gfx/metro/Tokyo/asakusa/toei5300/toei5300.png") { tmpl_std(0, 75) }

//*livery 1
  //special gfx
  //TODO skirt variant
  //panto-placers
    //var 1
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_panto_placer_1,position_in_vehid_chain %8){
      1: tk_as_toei5300_mot;
      4: tk_as_toei5300_mot;
      default: tk_as_toei5300_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_panto_placer_1_rev,position_in_vehid_chain_from_end %8){
      2: tk_as_toei5300_mot;
      5: tk_as_toei5300_mot;
      default: tk_as_toei5300_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_toei5300_panto_placer_main_1,vehicle_is_reversed){
      1: sw_tk_as_toei5300_panto_placer_1_rev;
      sw_tk_as_toei5300_panto_placer_1;
    }
    //var 2
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_panto_placer_2,position_in_vehid_chain %3){
      0: tk_as_toei5300_mot;
      default: tk_as_toei5300_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_panto_placer_2_rev,1-position_in_vehid_chain_from_end %3){
      0: tk_as_toei5300_mot;
      default: tk_as_toei5300_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_toei5300_panto_placer_main_2,vehicle_is_reversed){
      1: sw_tk_as_toei5300_panto_placer_2_rev;
      sw_tk_as_toei5300_panto_placer_2;
    }
    //panto controller
    switch(FEAT_TRAINS, SELF, sw_toei5300_panto_placer_main,num_vehs_in_vehid_chain+1){
      3: sw_toei5300_panto_placer_main_2;
      6: sw_toei5300_panto_placer_main_2;
      10..11: sw_toei5300_panto_placer_main_2;
      sw_toei5300_panto_placer_main_1;
    }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_gfx_main,position_in_vehid_chain_from_end){
        0: tk_as_toei5300_end;
        sw_toei5300_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_livery_1,vehicle_type_id){
        mu_car: sw_tk_as_toei5300_gfx_main;
        tk_as_toei5300_front;
    }
//post fixers
  //capacity-changer
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_capacity,position_in_vehid_chain_from_end){
      0: 123;
      default: 134;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_capacity_1,position_in_vehid_chain){
      0: 123;
      default: sw_tk_as_toei5300_capacity;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_capacity_main,vehicle_type_id){
      mu_car: sw_tk_as_toei5300_capacity;
      sw_tk_as_toei5300_capacity_1;
    }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_tk_as_toei5300_power_main,position_in_vehid_chain_from_end%8){
      0:return 165;
      2:return 165;
      5:return 165;
      7:return 165;
      return 0;
    }
//attachable vehicles
switch(FEAT_TRAINS, SELF, tk_as_toei5300_attach_vehid, vehicle_type_id) {
  mu_car_narrow: return string(STR_ATTACH_1435_ERROR);
  mu_car_metro: return string(STR_ATTACH_1435_ERROR);
  return CB_RESULT_ATTACH_ALLOW;
}

item(FEAT_TRAINS, tk_as_toei5300,1122) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_TOEI), "5300", string(STR_NAME_TYPE));
        introduction_date: date(1991, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 123;
        loading_speed: param_loading_3D;
        cost_factor: 19;
        running_cost_factor: 49;

        speed: 120 km/h;
        track_type: TR_1435_DC;
        power: 620 kW;
        weight: 24 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
    power: sw_tk_as_toei5300_power_main()*4*1342/1000;
        default: sw_tk_as_toei5300_livery_1;
		purchase: tk_as_toei5300_purchase;
		can_attach_wagon: tk_as_toei5300_attach_vehid;
		additional_text:
 		   	string(STR_TRAIN_DESC,
				string(STR_STANDARDGAUGE),
				string(STR_DENKI, string(STR_DC_N, 1500)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
				string(STR_FORMATION, string(STR_NUM_CARS_1, 8)),
				string(STR_OPERATOR, string(STR_TOEI)),
				string(STR_LINES_USED, string(STR_TOKYO_ASAKUSA)));

		start_stop: sw_stop_start_3;   
        cargo_capacity: sw_tk_as_toei5300_capacity_main();
        sound_effect: sw_TOEI5300_start_sound;
    }
	livery_override(mu_car){
		cargo_subtype_text:sw_empty_desc;
        loading_speed: param_loading_3D;
        cargo_capacity: sw_tk_as_toei5300_capacity_main();
        default: sw_tk_as_toei5300_livery_1;
        power: sw_tk_as_toei5300_power_main()*4*1342/1000;
        weight: 24;
        length: 8;
      }
}