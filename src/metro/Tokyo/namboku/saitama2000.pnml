//purchase
spriteset(tk_nm_saitama2000_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_nm_saitama2000_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_3(tk_nm_saitama2000_purchase, logo_saitama, tk_nm_saitama2000_purchase_c1, tk_nm_saitama2000_purchase_c2)

spriteset(tk_nm_saitama2000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_std(0, 0) }
spriteset(tk_nm_saitama2000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_std_rev(0, 25) }
spriteset(tk_nm_saitama2000_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_std_rev(0, 50) }
spriteset(tk_nm_saitama2000_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_std(0, 50) }
spriteset(tk_nm_saitama2000_mot_2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_symm(0, 75) }
spriteset(tk_nm_saitama2000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/namboku/saitama2000/saitama2000.png"){ tmpl_symm(0, 100) }

//*livery 1
  //panto-placers
    //var 1
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_panto_placer_1,position_in_vehid_chain %5){
      0: tk_nm_saitama2000_mot;
      2: tk_nm_saitama2000_mot_2;
      tk_nm_saitama2000_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_panto_placer_1_rev,position_in_vehid_chain_from_end %5 -1){
      0: tk_nm_saitama2000_mot_rev;
      2: tk_nm_saitama2000_mot_2;
      tk_nm_saitama2000_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_nm_saitama2000_panto_placer_main_1,vehicle_is_reversed){
      1: sw_tk_nm_saitama2000_panto_placer_1_rev;
      sw_tk_nm_saitama2000_panto_placer_1;
    }
    //var 2
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_panto_placer_2,position_in_vehid_chain %3){
      1: tk_nm_saitama2000_mot_2;
      tk_nm_saitama2000_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_panto_placer_2_rev,position_in_vehid_chain_from_end %3 -1){
      1: tk_nm_saitama2000_mot_2;
      tk_nm_saitama2000_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_nm_saitama2000_panto_placer_main_2,vehicle_is_reversed){
      1: sw_tk_nm_saitama2000_panto_placer_2_rev;
      sw_tk_nm_saitama2000_panto_placer_2;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_panto_placer_main,num_vehs_in_vehid_chain+1){
      5..6: sw_tk_nm_saitama2000_panto_placer_main_1;
      sw_tk_nm_saitama2000_panto_placer_main_2;
    }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_gfx_main,position_in_vehid_chain_from_end){
      0: tk_nm_saitama2000_end;
      default: sw_tk_nm_saitama2000_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_lv,vehicle_type_id){
      tk_nm_saitama2000: tk_nm_saitama2000_front;
      default: sw_tk_nm_saitama2000_gfx_main;
    }
//capacity-changer
  switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_capacity,position_in_vehid_chain_from_end){
    0: 140;
    150;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_capacity_1,position_in_vehid_chain){
    0: 140;
    sw_tk_nm_saitama2000_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_capacity_main,vehicle_type_id){
    mu_car_narrow: sw_tk_nm_saitama2000_capacity;
    sw_tk_nm_saitama2000_capacity_1;
  }
  //power fix
  switch(FEAT_TRAINS, SELF, sw_tk_nm_saitama2000_power_main,position_in_vehid_chain_from_end%6){
    1..2:return 190;
    4:return 190;
    return 0;
  }
  //attachable vehicles
  switch(FEAT_TRAINS, SELF, tk_nm_saitama2000_attach_vehid, vehicle_type_id) {
    mu_car: return string(STR_ATTACH_1067_ERROR);
    mu_car_metro: return string(STR_ATTACH_1067_ERROR);
    return CB_RESULT_ATTACH_ALLOW;
  }
//

item(FEAT_TRAINS, tk_nm_saitama2000,1115) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_SAITAMA), "2000", string(STR_NAME_SERIES));
        introduction_date: date(2000, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 146;
        loading_speed: param_loading_4D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 110 km/h;
        track_type: TR_1067_DC;
        power: 800 kW;
        weight: 30 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        length: 8;
    }
	graphics {
        power: sw_tk_nm_saitama2000_power_main()*4*1342/1000;
        default: sw_tk_nm_saitama2000_lv;
		purchase: tk_nm_saitama2000_purchase;
		can_attach_wagon: tk_nm_saitama2000_attach_vehid;
		additional_text:
 		   	string(STR_TRAIN_DESC,
				string(STR_NARROWGAUGE),
				string(STR_DENKI, string(STR_DC_N, 1500)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 4)),
				string(STR_FORMATION, string(STR_NUM_CARS_1, 6)),
				string(STR_OPERATOR, string(STR_TOKYOMETRO)),
				string(STR_LINES_USED, string(STR_TOKYO_NAMBOKU_SAITAMA)));

		start_stop: sw_stop_start_3;   
        cargo_capacity: sw_tk_nm_saitama2000_capacity_main()*param_capacity_mod/3;
    }
	livery_override(mu_car_narrow){
		cargo_subtype_text:sw_empty_desc;
        loading_speed: param_loading_4D;
        cargo_capacity: sw_tk_nm_saitama2000_capacity_main()*param_capacity_mod/3;
        default: sw_tk_nm_saitama2000_lv;
        power: sw_tk_nm_saitama2000_power_main()*4*1342/1000;
        weight: 30;
        length: 8;
      }
}