//purchase
spriteset(kt_ks_10series_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_purchase_1c(0, 0) }
spriteset(kt_ks_10series_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_4(kt_ks_10series_purchase, logo_kyoto_subway, logo_line_kt_karasuma, kt_ks_10series_purchase_c1, kt_ks_10series_purchase_c2)

spriteset(kt_ks_10series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_std(0, 0) }
spriteset(kt_ks_10series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_std_rev(0, 25) }
spriteset(kt_ks_10series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_std(0, 75) }
spriteset(kt_ks_10series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_std(0, 50) }
spriteset(kt_ks_10series_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_std(0, 100) }
spriteset(kt_ks_10series_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/10series/10series.png"){ tmpl_std_rev(0, 100) }

//livery 1
//panto placer
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_def,position_in_consist %3){
    2: kt_ks_10series_mot;
    kt_ks_10series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_def_rev,position_in_consist_from_end %3){
    2: kt_ks_10series_mot;
    kt_ks_10series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_10series_panto_def_rev_checker,vehicle_is_reversed){
    1: sw_kt_ks_10series_panto_placer_def_rev;
    sw_kt_ks_10series_panto_placer_def;
}
//special gfx
//4 car
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_4,position_in_consist %4){
    1: kt_ks_10series_mot;
    kt_ks_10series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_4_rev,position_in_consist_from_end %4){
    1: kt_ks_10series_mot;
    kt_ks_10series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_10series_panto_4_rev_checker,vehicle_is_reversed){
    1: sw_kt_ks_10series_panto_placer_4_rev;
    sw_kt_ks_10series_panto_placer_4;
}
//6 car
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_6,position_in_consist %6){
    1: kt_ks_10series_mot;
    4: kt_ks_10series_mot;
    kt_ks_10series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_6_rev,position_in_consist_from_end %6){
    1: kt_ks_10series_mot;
    4: kt_ks_10series_mot;
    kt_ks_10series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_10series_panto_6_rev_checker,vehicle_is_reversed){
    1: sw_kt_ks_10series_panto_placer_6_rev;
    sw_kt_ks_10series_panto_placer_6;
}

switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_panto_placer_main,num_vehs_in_consist){
    3..4:sw_kt_ks_10series_panto_4_rev_checker;
    6:sw_kt_ks_10series_panto_6_rev_checker;
    sw_kt_ks_10series_panto_def_rev_checker;
}

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_fake_placer,position_in_consist){
    3: kt_ks_10series_end_f;
    4: kt_ks_10series_front_f;
sw_kt_ks_10series_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_fake_placer_rev,position_in_consist_from_end){
    3: kt_ks_10series_front_f;
    4: kt_ks_10series_end_f;
sw_kt_ks_10series_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_10series_fake_placer_gfx,vehicle_is_reversed){
    1: sw_kt_ks_10series_fake_placer_rev;
    sw_kt_ks_10series_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_fake_placer_main,num_vehs_in_consist>7){
    1: sw_kt_ks_10series_fake_placer_gfx;
sw_kt_ks_10series_panto_placer_main;
}
//fake head-end placer end

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_end_gfx,position_in_consist_from_end){
    0: kt_ks_10series_end;
    default: sw_kt_ks_10series_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_lv,position_in_consist){
    0: kt_ks_10series_front;
    default: sw_kt_ks_10series_end_gfx;
}

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_kt_ks_10series_main_capacity,position_in_consist_from_end){
    0: 144*param_capacity_mod/3;
    default: 130*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, kt_ks_10series_attach_vehid, vehicle_type_id) {
  mu_car: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, kt_ks_10series,1350) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_kyoto == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_TRAIN_NAME, string(STR_NAME_KYOTO), "10", string(STR_NAME_SERIES));
        introduction_date: date(1980, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cost_factor: 20;
        running_cost_factor: 50;
        speed: 105 km/h;
        track_type: TR_1435_DC;
        power: 560 kW;
        weight: 35 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_kt_ks_10series_lv;
        purchase: kt_ks_10series_purchase;
        can_attach_wagon: kt_ks_10series_attach_vehid;
        additional_text:
    string(STR_TRAIN_DESC,
		string(STR_STANDARDGAUGE),
		string(STR_DENKI, string(STR_DC_N, 1500)),
		string(STR_LOAD_SPEED, string(STR_DOORS, 4)),
		string(STR_FORMATION, string(STR_NUM_CARS_2, 4,6)),
		string(STR_OPERATOR, string(STR_KYOTO)),
		string(STR_LINES_USED, string(STR_KYOTO_KARASUMA)));

        start_stop: sw_stop_start_3; 
        cargo_capacity: 130*param_capacity_mod/3;  
    }
	livery_override((mu_car)){
		cargo_subtype_text:sw_empty_desc;
        loading_speed: param_loading_4D;
        cargo_capacity: sw_kt_ks_10series_main_capacity;
        default: sw_kt_ks_10series_lv;
        power: 751*3/5;
        weight: 34;
        length: 8; 
      }
}