//purchase
spriteset(kt_ks_20series_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_purchase_1c(0, 0) }
spriteset(kt_ks_20series_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_purchase_2c(0, 50) }
GRAPHIC_STACK_4(kt_ks_20series_purchase, logo_kyoto_subway, logo_line_kt_karasuma, kt_ks_20series_purchase_c1, kt_ks_20series_purchase_c2)

spriteset(kt_ks_20series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std(0, 0) }
spriteset(kt_ks_20series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std_rev(0, 25) }
spriteset(kt_ks_20series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std(0, 75) }
spriteset(kt_ks_20series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std(0, 50) }
spriteset(kt_ks_20series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std_rev(0, 50) }
spriteset(kt_ks_20series_front_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std(0, 100) }
spriteset(kt_ks_20series_end_f, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Kyoto/karasuma/20series/20series.png"){ tmpl_std_rev(0, 100) }

//livery 1
//panto reverse check
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_gfx,vehicle_is_reversed){
    1: kt_ks_20series_mot_rev;
    kt_ks_20series_mot;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_def,position_in_consist %3){
    2: sw_kt_ks_20series_panto_gfx;
    kt_ks_20series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_def_rev,position_in_consist_from_end %3){
    2: sw_kt_ks_20series_panto_gfx;
    kt_ks_20series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_20series_panto_def_rev_checker,vehicle_is_reversed){
    1: sw_kt_ks_20series_panto_placer_def_rev;
    sw_kt_ks_20series_panto_placer_def;
}
//special gfx
//4 car
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_4,position_in_consist %4){
    1: sw_kt_ks_20series_panto_gfx;
    kt_ks_20series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_4_rev,position_in_consist_from_end %4){
    1: sw_kt_ks_20series_panto_gfx;
    kt_ks_20series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_20series_panto_4_rev_checker,vehicle_is_reversed){
    1: sw_kt_ks_20series_panto_placer_4_rev;
    sw_kt_ks_20series_panto_placer_4;
}
//6 car
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_6,position_in_consist %6){
    1: sw_kt_ks_20series_panto_gfx;
    4: sw_kt_ks_20series_panto_gfx;
    kt_ks_20series_wag;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_6_rev,position_in_consist_from_end %6){
    1: sw_kt_ks_20series_panto_gfx;
    4: sw_kt_ks_20series_panto_gfx;
    kt_ks_20series_wag;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_20series_panto_6_rev_checker,vehicle_is_reversed){
    1: sw_kt_ks_20series_panto_placer_6_rev;
    sw_kt_ks_20series_panto_placer_6;
}

switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_panto_placer_main,num_vehs_in_consist){
    3..4:sw_kt_ks_20series_panto_4_rev_checker;
    6:sw_kt_ks_20series_panto_6_rev_checker;
    sw_kt_ks_20series_panto_def_rev_checker;
}

//fake head-end placer
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_fake_placer,position_in_consist){
    3: kt_ks_20series_end_f;
    4: kt_ks_20series_front_f;
sw_kt_ks_20series_panto_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_fake_placer_rev,position_in_consist_from_end){
    3: kt_ks_20series_front_f;
    4: kt_ks_20series_end_f;
sw_kt_ks_20series_panto_placer_main;
}
switch(FEAT_TRAINS, PARENT, sw_kt_ks_20series_fake_placer_gfx,vehicle_is_reversed){
    1: sw_kt_ks_20series_fake_placer_rev;
    sw_kt_ks_20series_fake_placer;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_fake_placer_main,num_vehs_in_consist>8){
    1: sw_kt_ks_20series_fake_placer_gfx;
sw_kt_ks_20series_panto_placer_main;
}
//fake head-end placer end

//front-end-fixer
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_end_gfx,position_in_consist_from_end){
    0: kt_ks_20series_end;
    default: sw_kt_ks_20series_fake_placer_main;
}
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_livery_1,position_in_consist){
    0: kt_ks_20series_front;
    default: sw_kt_ks_20series_end_gfx;
}

//capacity-changer
switch(FEAT_TRAINS, SELF, sw_kt_ks_20series_main_capacity,position_in_consist_from_end){
    0: 130*param_capacity_mod/3;
    default: 143*param_capacity_mod/3;
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, kt_ks_20series_attach_vehid, vehicle_type_id) {
  mu_car: return CB_RESULT_ATTACH_ALLOW;
  return string(STR_ATTACH_ERROR);
}

item(FEAT_TRAINS, kt_ks_20series,1351) {
    property {
        cargo_capacity: 130;
        climates_available: param_disable_kyoto == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.2;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;

        name: string(STR_TRAIN_NAME, string(STR_NAME_KYOTO), "20", string(STR_NAME_SERIES));
        introduction_date: date(2022, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cost_factor: 22;
        running_cost_factor: 52;
        speed: 105 km/h;
        track_type: TR_1435_DC;
        power: 560 kW;
        weight: 30 ton;
        loading_speed: param_loading_4D;
    }
	graphics {
        default: sw_kt_ks_20series_livery_1;
        purchase: kt_ks_20series_purchase;
        can_attach_wagon: kt_ks_20series_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION, string(STR_NUM_CARS_1, 6)),
                            string(STR_OPERATOR, 
                            string(STR_KYOTO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_KYOTO_KARASUMA)
                            ),
                        );
        start_stop: sw_stop_start_3; 
        cargo_capacity: 130*param_capacity_mod/3;  
    }
    livery_override(mu_car){
        loading_speed: param_loading_4D;
        cargo_capacity: sw_kt_ks_20series_main_capacity;
        default:                      sw_kt_ks_20series_livery_1;
        power: 751*3/5;
        weight: 33;
        length: 8; 
      }
}